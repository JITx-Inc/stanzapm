defpackage stz/bindings :
  import core
  import collections
  import macro-utils
  import reader
  import parser
  import stz/core-macros

;============================================================
;=================== Bindings Structure =====================
;============================================================

public defstruct Bindings :
  externs: Tuple<ExternBinding>
  extern-defns: Tuple<ExternDefnBinding>
with:
  printer => true

public defstruct ExternDefnBinding :
  lbl: Symbol
with:
  printer => true

public defstruct ExternBinding :
  lbl: Symbol
with:
  printer => true

;============================================================
;======================= Reader =============================
;============================================================

public defn read-bindings (filename:String) -> Bindings :
  parse-syntax[extern-bindings / #bindings](read-file(filename))

;============================================================
;===================== Syntax ===============================
;============================================================

defsyntax extern-bindings :
  import (id!) from core
  defproduction entry! : ExternDefnBinding|ExternBinding
  defrule entry! = (extern defn ?name:#id!) : ExternDefnBinding(unwrap-token(name))
  defrule entry! = (extern ?name:#id!) : ExternBinding(unwrap-token(name))
  fail-if entry! = () : ReadBindingError(closest-info(), "Expected an extern declaration here.")

  public defproduction bindings : Bindings
  defrule bindings = (?es:#entry! ...) :
    Bindings(
      to-tuple(filter-by<ExternBinding>(es))
      to-tuple(filter-by<ExternDefnBinding>(es)))

;============================================================
;====================== Exception ===========================
;============================================================

public defstruct ReadBindingError <: Exception :
  info: FileInfo|False
  msg: String

defmethod print (o:OutputStream, e:ReadBindingError) :
  print(o, "%_: " % [info(e)]) when info(e) is-not False    
  print(o, msg(e))