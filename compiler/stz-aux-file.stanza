#use-added-syntax(stz-serializer-lang)
defpackage stz/aux-file :
  import core
  import collections
  import stz/serializer
  import stz/utils
  import stz/front-end
  import stz/proj-manager

;============================================================
;==================== Aux File Definition ===================
;============================================================

public deftype AuxFile
public defmulti key? (f:AuxFile, r:PkgRecord) -> True|False
public defmulti target-up-to-date? (f:AuxFile, target:Symbol, settings:BuildRecordSettings) -> True|False
public defmulti add (f:AuxFile, r:AuxRecord) -> False
public defmulti save (f:AuxFile) -> False

public defn AuxFile (path:String) -> AuxFile :
  val records = read-aux-records(path)
  val new-records = Vector<AuxRecord>()
  new AuxFile :
    defmethod key? (this, r:PkgRecord) :
      contains?(filter-by<PkgRecord>(/records(records)), r)
    defmethod target-up-to-date? (this, target:Symbol, settings:BuildRecordSettings) :
      val rec = for r in filter-by<BuildRecord>(/records(records)) find : /target(r) == target
      match(rec:BuildRecord) :
        /settings(rec) == settings and
        all?(up-to-date?, packages(rec)) and
        all?(up-to-date?, files(rec))        
    defmethod add (this, r:AuxRecord) :
      add(new-records, r)
    defmethod save (this) :
      val records* = combine(records, new-records)
      write-aux-records(path, records*)

public defn AuxFile () :
  AuxFile("stanza.aux2")

;============================================================
;=================== Check Up-to-Date =======================
;============================================================

defn timestamp? (file:String|False) :
  match(file:String) :
    time-modified(file) when file-exists?(file)

defn up-to-date? (s:FileStamp) :
  timestamp?(filename(s)) == timestamp(s)

defn up-to-date? (s:PackageStamp) :
  val l = location(s)
  if read-pkg?(l) :
    timestamp?(pkg-file(l)) == pkg-timestamp(s) and
    timestamp?(source-file(l)) == source-timestamp(s)
  else :
    timestamp?(source-file(l)) == source-timestamp(s)

;============================================================
;=================== Aux Serializer =========================
;============================================================

defn write-aux-records (name:String, auxfile:AuxRecords) :
  val f = FileOutputStream(name)
  try : serialize(f, auxfile)
  finally : close(f)

defn read-aux-records (name:String) -> AuxRecords :
  val f = FileInputStream(name)
  try : deserialize-auxrecords(f)
  catch (e:DeserializeException) : throw(CorruptedAuxFile(name))
  finally : close(f)

;============================================================
;===================== Aux File Structure ===================
;============================================================

defstruct AuxRecords :
  records: Tuple<AuxRecord>

public deftype AuxRecord

public defstruct PkgRecord <: AuxRecord & Hashable & Equalable :
  package: Symbol
  filestamp: FileStamp
  source-stamp: FileStamp
  flags: Tuple<Symbol>
  optimize?: True|False
with:
  constructor => #PkgRecord

public defstruct BuildRecord <: AuxRecord :
  target: Symbol
  packages: Tuple<PackageStamp>
  files: Tuple<FileStamp>
  settings: BuildRecordSettings
with:
  printer => true

public defstruct BuildRecordSettings <: AuxRecord & Hashable & Equalable :
  inputs: Tuple<String|Symbol>
  vm-packages: Tuple<String|Symbol>
  platform: Symbol|False
  assembly: String|False
  output: String|False
  pkg-dir: String|False
  optimize?: True|False
  ccfiles: Tuple<String>
  ccflags: Tuple<String>
  flags: Tuple<Symbol>  
with:
  printer => true

;------------------------------------------------------------
;---------------- Canonical Constructor ---------------------
;------------------------------------------------------------

public defn PkgRecord (package:Symbol,
                       filestamp:FileStamp,
                       source-stamp:FileStamp,
                       flags:Tuple<Symbol>,
                       optimize?:True|False) :
  #PkgRecord(package, filestamp, source-stamp,
             qsort(flags), optimize?)

;------------------------------------------------------------
;------------------ Hashing and Equality --------------------
;------------------------------------------------------------

defn key (r:PkgRecord) :
  [package(r), filestamp(r), source-stamp(r), flags(r), optimize?(r)]

defmethod equal? (a:PkgRecord, b:PkgRecord) : key(a) == key(b)
defmethod hash (r:PkgRecord) : hash $ key(r)

defn key (r:BuildRecordSettings) :
  [inputs(r), vm-packages(r), platform(r), assembly(r), output(r),
   pkg-dir(r), optimize?(r), ccfiles(r), ccflags(r), flags(r)]

defmethod equal? (a:BuildRecordSettings, b:BuildRecordSettings) : key(a) == key(b)
defmethod hash (r:BuildRecordSettings) : hash $ key(r)

;============================================================
;======================== Printer ===========================
;============================================================

defmethod print (o:OutputStream, f:AuxRecords) :
  print(o, "auxrecords :")
  val o2 = IndentedStream(o)
  do(lnprint{o2, _}, records(f))

defmethod print (o:OutputStream, r:PkgRecord) :
  print(o, "package %~ at %%_ defined-in %_ with flags (%@) using optimize %_" % [
    package(r), filestamp(r), source-stamp(r), flags(r), optimize?(r)])

;============================================================
;================== Combining Records =======================
;============================================================

defn combine (file:AuxRecords, records:Seqable<AuxRecord>) :
  val pkg-records = HashTable<String,PkgRecord>()
  val build-records = HashTable<Symbol,BuildRecord>()
  for r in cat(/records(file), records) do :
    match(r) :
      (r:PkgRecord) :
        pkg-records[filename(filestamp(r))] = r
      (r:BuildRecord) :
        build-records[target(r)] = r
  val records* = to-tuple $ cat(
    values(pkg-records),
    values(build-records))
  AuxRecords(records*)

;============================================================
;================= Serializer Definition ====================
;============================================================

defserializer (out:FileOutputStream, in:FileInputStream) :

  ;----------------------------------------------------------
  ;--------------------- Records ----------------------------
  ;----------------------------------------------------------
  defunion auxrecords (AuxRecords) :
    AuxRecords: (records:tuple(auxrecord))

  defunion auxrecord (AuxRecord) :
    PkgRecord: (package:symbol, filestamp:filestamp, source-stamp:filestamp,
                flags:tuple(symbol), optimize?:bool)
    BuildRecord: (target:symbol, packages:tuple(pkgstamp), files:tuple(filestamp), settings:build-record-settings)            

  defunion build-record-settings (BuildRecordSettings) :
    BuildRecordSettings: (inputs:tuple(string-or-symbol), vm-packages:tuple(string-or-symbol),
                          platform:opt<Symbol>(symbol), assembly:opt<String>(string), output:opt<String>(string),
                          pkg-dir:opt<String>(string), optimize?:bool, ccfiles:tuple(string), ccflags:tuple(string), flags:tuple(symbol))

  defunion pkgstamp (PackageStamp) :
    PackageStamp: (location:pkglocation, source-timestamp:opt<Long>(long), pkg-timestamp:opt<Long>(long))

  defunion pkglocation (PkgLocation) :
    PkgLocation: (package:symbol, source-file:opt<String>(string), pkg-file:opt<String>(string), read-pkg?:bool) 

  defunion filestamp (FileStamp) :
    FileStamp: (filename:string, timestamp:long)

  ;----------------------------------------------------------
  ;-------------------- Combinators -------------------------
  ;----------------------------------------------------------
  
  reader defn read-tuple<?T> (f: () -> ?T) :
    val n = read-int()
    to-tuple(repeatedly(f, n))

  writer defn write-tuple<?T> (f: T -> False, xs:Tuple<?T>) :
    write-int(length(xs))
    do(f, xs)

  reader defn read-opt<T> (f: () -> T) :
    val n = read-byte()
    f() when n == 1Y

  writer defn write-opt<T> (f: T -> False, x:T|False) :
    match(x) :
      (x:False) :
        write-byte(0Y)
      (x:T) :
        write-byte(1Y)
        f(x)

  ;----------------------------------------------------------
  ;----------------------- Atoms ----------------------------
  ;----------------------------------------------------------
  defatom bool (x:True|False) :
    writer :
      match(x) :
        (x:True) : put(out, 1Y)
        (x:False) : put(out, 0Y)
    reader :
      switch(get-byte(in)) :
        1Y : true
        0Y : false
        else : throw(DeserializeException())

  defatom byte (x:Byte) :
    writer :
      put(out, x)
    reader :
      match(get-byte(in)) :
        (x:Byte) : x
        (x:False) : throw(DeserializeException())
  
  defatom int (x:Int) :
    writer :
      put(out, x)
    reader :
      match(get-int(in)) :
        (x:Int) : x
        (x:False) : throw(DeserializeException())

  defatom long (x:Long) :
    writer :
      put(out, x)
    reader :
      match(get-long(in)) :
        (x:Long) : x
        (x:False) : throw(DeserializeException())

  defatom string (x:String) :
    writer :
      write-int(length(x))
      print(out, x)
    reader :
      val n = read-int()
      String(repeatedly(read-char, n))

  defatom symbol (x:Symbol) :
    writer :
      write-string(to-string(x))
    reader :
      to-symbol(read-string())

  defatom char (x:Char) :
    writer :
      print(out, x)
    reader :
      match(get-char(in)) :
        (x:Char) : x
        (x:False) : throw(DeserializeException())

  defatom string-or-symbol (x:String|Symbol) :
    writer :
      match(x) :
        (x:String) :
          put(out, 0Y)
          write-string(x)
        (x:Symbol) :
          put(out, 1Y)
          write-symbol(x)
    reader :
      switch(get-byte(in)) :
        0Y : read-string()
        1Y : read-symbol()
        else : throw(DeserializeException())

;============================================================
;====================== Exceptions ==========================
;============================================================

public defstruct CorruptedAuxFile <: Exception :
  filename: String

defmethod print (o:OutputStream, f:CorruptedAuxFile) :
  print(o, "Auxiliary file %~ is corrupted." % [filename(f)])