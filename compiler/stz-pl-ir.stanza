defpackage stz/pl-ir :
  import core
  import collections
  import stz/tl-ir

#use-added-syntax(stz-ast-lang, stz-printer-lang)

;============================================================
;===================== Structures ===========================
;============================================================

public defstruct PkgIds :
  declarations: List<Declare>
  imports: List<ImportPackage>

public defstruct Declare :
  n: Int
  id: Id
  info: FileInfo

public defstruct ImportPackage :
  package: Symbol
  imports: List<Import>

public defstruct Import :
  n: Int
  id: Id

public defast :
  ;Visibility
  deftype Visibility
  defnodes Visibility :
    Private
    Protected
    Public

  ;Identification Records
  deftype Id
  defnodes Id :
    ValId :
      name: Symbol
      type: Type
      visibility: Visibility
    VarId :
      name: Symbol
      type: Type
      visibility: Visibility
    FnId :
      name: Symbol
      tail?: True|False
      targs: List<Int>
      cargs: List<Int>
      a1: List<Type>
      a2: Type
      visibility: Visibility
    MultiId :
      name: Symbol
      tail?: True|False
      targs: List<Int>
      cargs: List<Int>
      a1: List<Type>
      a2: Type
      visibility: Visibility
    TypeId :
      name: Symbol
      args: List<Int>
      parent: False|Type
      children: List<ChildId>
      visibility: Visibility
    TArgId :
      name: Symbol      

public defstruct ChildId :
  n: Int
  args: List<Int>
  parent: Type

;============================================================
;====================== Printers ============================
;============================================================

defprinter (p:PkgIds) :
  PkgIds :
    ($pkg (declarations ...) (imports ...))

defprinter (d:Declare) :
  Declare :
    ($def n id info)

defprinter (d:ImportPackage) :
  ImportPackage :
    ($import-package package imports ...)
    
defprinter (d:Import) :
  Import :
    ($import n id)

defprinter (c:ChildId) :
  ChildId :
    ($child n (args ...) parent)

defprinter (v:Visibility) :
  Private :
    ($private)
  Protected :
    ($protected)
  Public :
    ($public)

defprinter (id:Id) :
  ValId :
    ($val name type visibility)
  VarId :
    ($var name type visibility)
  FnId :
    if tail?(id) : ($fn* name (targs ...) (cargs ...) (a1 ...) a2 visibility)
    else : ($fn name (targs ...) (cargs ...) (a1 ...) a2 visibility)
  MultiId :
    if tail?(id) : ($multi* name (targs ...) (cargs ...) (a1 ...) a2 visibility)
    else : ($multi name (targs ...) (cargs ...) (a1 ...) a2 visibility)    
  TypeId :
    ($type name (args ...) parent (children ...) visibility)
  TArgId :
    ($targ name)