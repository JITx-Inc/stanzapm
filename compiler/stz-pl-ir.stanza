defpackage stz/pl-ir :
  import core
  import collections
  import stz/tl-ir

#use-added-syntax(stz-ast-lang, stz-printer-lang)

;============================================================
;===================== Structures ===========================
;============================================================

public defstruct PkgIds :
  declarations: List<Declare>
  imports: List<ImportPackage>

public defstruct Declare :
  n: Int
  id: Id
  info: FileInfo

public defstruct ImportPackage :
  package: Symbol
  imports: List<Import>

public defstruct Import :
  n: Int
  id: Id

public defast :
  ;Visibility
  deftype Visibility
  defnodes Visibility :
    Private
    Protected
    Public

  ;Identification Records
  deftype Id
  defnodes Id :
    ValId :
      name: Symbol
      type: Type
      visibility: Visibility
    VarId :
      name: Symbol
      type: Type
      visibility: Visibility
    FnId :
      name: Symbol
      tail?: True|False
      targs: List<Int>
      cargs: List<Int>
      a1: List<Type>
      a2: Type
      visibility: Visibility
    MultiId :
      name: Symbol
      tail?: True|False
      targs: List<Int>
      cargs: List<Int>
      a1: List<Type>
      a2: Type
      visibility: Visibility
    TypeId :
      name: Symbol
      args: List<Int>
      parent: False|Type
      children: List<ChildDec>
      visibility: Visibility
    TArgId :
      name: Symbol
      
    LSValId :
      name: Symbol
      type: LSType
      visibility: Visibility
    LSVarId :
      name: Symbol
      type: LSType
      visibility: Visibility
    LSFnId :
      name: Symbol
      tail?: True|False
      targs: List<Int>
      cargs: List<Int>
      a1: List<LSType>
      a2: LSType
      visibility: Visibility
    LSTypeId :
      name: Symbol
      args: List<Int>
      parent: False|Type
      fields: List<FieldDec>
      visibility: Visibility
    FieldId :
      name: Symbol

public defstruct ChildDec :
  n: Int
  args: List<Int>
  parent: Type

public defstruct FieldDec :
  n: Int
  type: LSType

;============================================================
;====================== Printers ============================
;============================================================

defprinter (p:PkgIds) :
  PkgIds :
    ($pkg (declarations ...) (imports ...))

defprinter (d:Declare) :
  Declare :
    ($def n id info)

defprinter (d:ImportPackage) :
  ImportPackage :
    ($import-package package imports ...)
    
defprinter (d:Import) :
  Import :
    ($import n id)

defprinter (c:ChildDec) :
  ChildDec :
    ($child n (args ...) parent)

defprinter (c:FieldDec) :
  FieldDec :
    ($field n type)

defprinter (v:Visibility) :
  Private :
    ($private)
  Protected :
    ($protected)
  Public :
    ($public)

defprinter (id:Id) :
  ValId :
    ($val name type visibility)
  VarId :
    ($var name type visibility)
  FnId :
    if tail?(id) : ($fn* name (targs ...) (cargs ...) (a1 ...) a2 visibility)
    else : ($fn name (targs ...) (cargs ...) (a1 ...) a2 visibility)
  MultiId :
    if tail?(id) : ($multi* name (targs ...) (cargs ...) (a1 ...) a2 visibility)
    else : ($multi name (targs ...) (cargs ...) (a1 ...) a2 visibility)    
  TypeId :
    ($type name (args ...) parent (children ...) visibility)
  TArgId :
    ($targ name)

  LSValId :
    ($lsval name type visibility)
  LSVarId :
    ($lsvar name type visibility)
  LSFnId :
    if tail?(id) : ($lsfn* name (targs ...) (cargs ...) (a1 ...) a2 visibility)
    else : ($lsfn name (targs ...) (cargs ...) (a1 ...) a2 visibility)    
  LSTypeId :
    ($lstype name (args ...) parent (fields ...) visibility)
  FieldId :
    ($field name)

  