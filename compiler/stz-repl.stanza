defpackage stz/repl :
  import core
  import collections
  import reader
  import parser
  import macro-utils
  import stz/utils
  import stz/il-ir
  import lang/check
  import stz/dl-ir with:
    prefix(Import) => DL
  import stz/tl-ir
  import stz/el-ir
  import stz/vm-ir
  import stz/vm
  import stz/core-macros
  import stz/input
  import stz/renamer
  import stz/resolver
  import stz/type
  import stz/tl-to-el
  import stz/dl
  import stz/el
  import stz/el-to-vm
  import stz/algorithms
  import line-noise/prompter
  import stz/pkg
  import stz/ids
  import stz/params
  import stz/proj-manager
  import stz/proj
  import stz/front-end with:
    prefix(FileAssoc) => proj-

;<algorithm>=================================================
;====================== Algorithms ==========================
;============================================================

# Iteratively loading files #

When attempting to load a set of input source files, it is possible
that they are inconsistent with some already loaded packages. For
example, a previously loaded package 'oldpackage' may be expecting the
function 'f' in 'mypackage' to return an 'Int', and you have just
changed 'f' in 'mypackage.stanza' to return a 'String'.

Instead of simply erroring at this stage, we would like to iteratively
load in 'oldpackage.stanza' and try compiling it against the new
definition of 'f'. Maybe it works! And even if it doesn't work, we'll
get a better error message than before.

Input:
  files: Tuple<String>
  update?: True|False
Output:
  epackages: Tuple<EPackage|Pkg>

If update? is true, then we assume that we are only updating the code
definitions, and that the global variables will not be re-initialized.

;============================================================
;=================================================<algorithm>

;============================================================
;===================== REPL Language ========================
;============================================================

public deftype RExp
public defstruct Load <: RExp :
  inputs: Tuple<String|Symbol>
with:
  printer => true
public defstruct Update <: RExp
with:
  printer => true
public defstruct Reload <: RExp
with:
  printer => true
public defstruct Clear <: RExp
with:
  printer => true
public defstruct Import <: RExp :
  import: IImport
with:
  printer => true
public defstruct Inside <: RExp :
  package: Symbol|False
with:
  printer => true
public defstruct Unimport <: RExp :
  package: Symbol
with:
  printer => true
public defstruct Eval <: RExp :
  form
with:
  printer => true
public defstruct NoOp <: RExp
with:
  printer => true

defstruct ReplSyntaxError <: Exception :
  info: FileInfo|False
  message: String
defmethod print (o:OutputStream, e:ReplSyntaxError) :
  val infostr = "" when info(e) is False else "%_: " % [info(e)]
  print-all(o, [infostr, message(e)])

defsyntax repl :
  public defproduction rexp : RExp
  defrule rexp = (?x:#@rexp #E) : x

  defproduction E : False
  defrule E != (_)
  defrule E = () : false

  public defproduction @rexp : RExp
  defrule @rexp = (load ?input0:#input ?inputs:#input! ...) :
    Load(to-tuple(cons(input0, inputs)))
  defrule @rexp = (?e:#import-clause #E) :
    Import(e)
  defrule @rexp = (inside - #E) :
    Inside(false)
  defrule @rexp = (inside ?p:#symbol) :
    Inside(p)
  defrule @rexp = (unimport ?name:#symbol) :
    Unimport(name)
  defrule @rexp = (update #E) :
    Update()
  defrule @rexp = (reload #E) :
    Reload()
  defrule @rexp = (clear #E) :
    Clear()
  defrule @rexp = (?forms ...) :
    if empty?(forms) : NoOp()
    else : Eval(forms)

  ;===== Inputs =====
  defproduction input : String|Symbol
  defrule input = (?file:#string) : file
  defrule input = (?pkg:#symbol) : pkg

  defproduction input! : String|Symbol
  defrule input! = (?i:#input) : i
  fail-if input! = () : ReplSyntaxError(closest-info(), "Input must be either a string or a symbol.")

  ;===== Imports =====
  defproduction prefix-clause! : IPrefix
  defrule prefix-clause! = (prefix => ?p:#symbol!) :
    IPrefix(false, to-string(p))
  defrule prefix-clause! = (prefix(?names:#symbol! ...) => ?p:#symbol!) :
    IPrefix(to-tuple(names), to-string(p))
  fail-if prefix-clause! = () :
    ReplSyntaxError(closest-info(), "Expected a prefix clause here.")

  defproduction prefix-clauses : Tuple<IPrefix>
  defrule prefix-clauses = (with #:! (?ps:#prefix-clause! ...)) : to-tuple(ps)
  defrule prefix-clauses = () : []

  defproduction import-clause : IImport
  defrule import-clause = (import ?package:#symbol ?ps:#prefix-clauses) :
    IImport(package, ps)

  ;===== Primitives =====
  defproduction int : Int
  defrule int = (?x) when unwrap-token(x) is Int : unwrap-token(x)

  defproduction byte : Byte
  defrule byte = (?x) when unwrap-token(x) is Byte : unwrap-token(x)

  defproduction string : String
  defrule string = (?x) when unwrap-token(x) is String : unwrap-token(x)

  defproduction symbol : Symbol
  defrule symbol = (?x) when unwrap-token(x) is Symbol : unwrap-token(x)

  defproduction string! : String
  defrule string! = (?x:#string) : x
  fail-if string! = () : ReplSyntaxError(closest-info(), "Expected a string here.")

  defproduction symbol! : Symbol
  defrule symbol! = (?x:#symbol) : x
  fail-if symbol! = () : ReplSyntaxError(closest-info(), "Expected a symbol here.")

  defproduction :!
  defrule :! = (?x: ~ :) : x
  fail-if :! = () : ReplSyntaxError(closest-info(), "Colon expected here.")

defn read-repl-exp (read-line:() -> List<Token>|False) -> RExp|False :
  defn read-line! () :
    try : read-line()
    catch (e:Exception) : throw(ReplErrors([e]))
  try :
    let loop () :
      match(read-line!()) :
        (forms:List) :
          match(parse-syntax[repl / #rexp](forms)) :
            (e:NoOp) : loop()
            (e) : e
        (forms:False) :
          false
  catch (e:LexerException) :
    throw(ReplErrors([e]))
  catch (e:NoMatchException) :
    throw(ReplErrors(causes(e)))

defn read-repl-exp (inputstream:InputStream) :
  defn read-input-line () : /read-line(inputstream)
  read-repl-exp(read-input-line)

defn read-repl-exp () :
  defn read-input-line () :
    read-line(LineNoisePrompter("stanza> ", "      > "))
  read-repl-exp(read-input-line)

;============================================================
;======================= Loading ============================
;============================================================

public deftype REPL
defmulti load-files (repl:REPL, inputs:Tuple<String|Symbol>) -> False
defmulti update-files (repl:REPL) -> False
defmulti reload (repl:REPL) -> False
defmulti load-repl (repl:REPL, form) -> False
defmulti import (repl:REPL, import:IImport) -> False
defmulti unimport (repl:REPL, package:Symbol) -> False
defmulti inside (repl:REPL, package:Symbol|False) -> False
defmulti clear (repl:REPL) -> False

public defn REPL () :
  ;============================================================
  ;===================== REPL State ===========================
  ;============================================================
  val vm = VirtualMachine()
  val denv = DEnv()
  val repl-env = REPLEnv()
  val file-env = FileEnv()
  val proj-files = Vector<String>()

  ;============================================================
  ;===================== Project Manager ======================
  ;============================================================
  add(proj-files, "stanza.proj")
  
  defn proj-manager () :
    val proj = read-proj-files(proj-files, OUTPUT-PLATFORM)
    val auxfile = read-aux-file?("stanza.aux2")
    val params = ProjParams(compiler-flags(), false, false)
    ProjManager(proj, auxfile, params)

  defn add-proj-file (file:String) :
    add(proj-files, file)

  ;============================================================
  ;===================== File Loading =========================
  ;============================================================
  ;Load files
  defn load-files (inputs:Tuple<String|Symbol>) :
    if not empty?(inputs) :
      ;Load project files separately
      val inputs* = Vector<String|Symbol>()
      for input in inputs do :
        match(input:String) :
          if suffix?(input, ".proj") : add-proj-file(input)
          else : add(inputs*, input)
        else : add(inputs*, input)

      within intercept-errors() :
        ;Compile to epackages
        val proj-manager = proj-manager()
        val result = /compile-to-el $ new FrontEndInputs :
          defmethod inputs (this) : to-tuple(inputs*)
          defmethod find-package (this, name:Symbol) : find-package(proj-manager, name)
          defmethod source-file? (this, name:Symbol) : source-file?(proj-manager, name)
          defmethod environment-package? (this, name:Symbol) : packageio(denv, name)
          defmethod load-into-denv (this, ios:Tuple<PackageIO>) :
            val live = compute-live(vm, seq(package, ios))
            load(denv, ios, live)

        ;Get all compiled pkgs
        val all-pkgs = to-tuple(cat(epackages(result), pkgs(result)))        

        ;Register file associations with file environment.
        for entry in group-by(filename, assocs(result)) do :
          val assoc = head(value(entry))
          val packages = to-tuple $ seq(package, value(entry))
          register(file-env, FileAssoc(filename(assoc), timestamp(assoc), packages))
        
        ;Register import lists with the repl environment.
        do(register{repl-env, import-list(_)}, ipackages(result))

        ;Compile to vmpackages
        val vmpackages = compile-vmpackages(all-pkgs)

        ;Load packages into virtual machine
        load(vm, vmpackages, false)

        ;Run packages in initialization order
        run-in-init-order(vmpackages, input-ordering(result))

  ;Ensure that a package is loaded
  defn ensure-package-loaded (package:Symbol) :
    within intercept-errors() :
      if packageio(denv,package) is False :
        ;Load the desired package
        val pkgloader = PkgLoader(false)
        load-pkg(pkgloader, package)

        ;And load all of its dependencies
        load-dependencies(pkgloader, seq(/package,packageios(denv)))
        val all-pkgs = pkgs(pkgloader)
        
        ;Load into denv
        load-into-denv!(pkgs(pkgloader))

        ;Load packages into virtual machine
        val vmpackages = map(vmp{_ as StdPkg}, pkgs(pkgloader))
        load(vm, vmpackages, false)
        
        ;Run packages in initialization order
        run-in-init-order(vmpackages, [package])

  ;Clear all repl definitions
  defn clear-repl () :  
    ;Compute what live repl records are required
    val live = compute-live(vm, repl-packages(repl-env))
    ;Unload from DEnvironment
    match(unload(denv, repl-packages(repl-env), live)) :
      (e:LoadErrors) : throw(ReplErrors(errors(e)))
      (e:LoadSuccess) : false
    ;Now clear the repl packages
    unload(vm, repl-packages(repl-env))
    clear(repl-env)

  ;Update files
  defn update-files () :
    within intercept-errors() :
      val changed-files = changed-files(file-env)
      if empty?(changed-files) :
        throw(ReplErrors([NoChangedFiles()]))
      else :
        val epackages = iteratively-load(changed-files, `exclude-none)
        val vmpackages = compile-vmpackages(epackages)
        load(vm, vmpackages, true)

  ;Reload
  defn reload-files () :
    within intercept-errors() :
      val changed-files = changed-files(file-env)
      val epackages = iteratively-load(changed-files, `exclude-all)
      val vmpackages = compile-vmpackages(epackages)
      load(vm, vmpackages, false)

      ;Clear all current globals
      clear-globals(vm)
      
      ;Clear all REPL definitions
      unload(denv, repl-packages(repl-env), []) as LoadSuccess
      unload(vm, repl-packages(repl-env))
      clear(repl-env)
      
      ;Rerun package initializers
      rerun-all-packages()

  ;Load from repl
  defn load-repl (form) :
    within intercept-errors() :
      val ipackage = to-ipackage(form)
      val proj-manager = proj-manager()
      val result = /compile-to-el $ new FrontEndInputs :
        defmethod inputs (this) : [ipackage]
        defmethod find-package (this, name:Symbol) : find-package(proj-manager, name)
        defmethod source-file? (this, name:Symbol) : source-file?(proj-manager, name)
        defmethod environment-package? (this, name:Symbol) : packageio(denv, name)
        defmethod transient? (this) : true
        defmethod package-priority (this) : package-priority(repl-env, name(ipackage))
        defmethod load-into-denv (this, ios:Tuple<PackageIO>) :
          val live = compute-live(vm, seq(package, ios))
          load(denv, ios, live)
          
      ;Get all compiled pkgs    
      val all-pkgs = to-tuple(cat(epackages(result), pkgs(result)))

      ;Register successful load
      register-repl(repl-env, name(ipackage))

      ;Compile to vmpackages
      val vmpackages = compile-vmpackages(all-pkgs)

      ;Load packages into virtual machine
      load(vm, vmpackages, false)

      ;Run packages in initialization order
      run-in-init-order(vmpackages, input-ordering(result))

  ;============================================================
  ;==================== Stages of Loading =====================
  ;============================================================
  defn iteratively-load (filenames:Tuple<String>, exclude-as-live:Symbol) -> Tuple<EPackage|Pkg> :
    val pkgloader = PkgLoader(false)
    val ipackages = Vector<IPackage>()
    val file-assocs = Vector<FileAssoc>()
    for filename in filenames do :
      val ipkgs = read-ipackages(filename)
      add-all(ipackages, ipkgs)
      add(file-assocs, FileAssoc(filename, time-modified(filename), map(name,ipkgs)))    
    val epackages = compile-to-el(ipackages, pkgloader, false)
    val result = load-into-denv(epackages, exclude-as-live)
    match(result:LoadErrors) :
      ;Some errors can be recovered from by including their source
      ;files during the recompilation. Other errors are unrecoverable.
      ;Classify these errors now.
      val more-source-files = Vector<String>()
      val unrecoverable-errors = Vector<LoadError>()
      for e in errors(result) do :
        match(source(e)) :
          (src:LoadedPackage) :
            match(source-file?(file-env, package(src))) :
              (file:String) : add(more-source-files, file)
              (file:False) : add(unrecoverable-errors, e)
          (src:LiveObject) :
            add(unrecoverable-errors, e)
      ;If there are no unrecoverable errors, then try again with the added source files
      if empty?(unrecoverable-errors) :
        val filenames* = to-tuple(unique(cat(filenames, more-source-files)))
        ;If the set of files have changed, then try again, otherwise nothing we can do.
        if length(filenames*) > length(filenames) : iteratively-load(filenames*, exclude-as-live)
        else : throw(ReplErrors(errors(result)))
      else :
        throw(ReplErrors(unrecoverable-errors))
    else :
      ;Register new files with file environment
      ;Register import lists with the repl environment
      do(register{file-env, _}, file-assocs)
      do(register{repl-env, import-list(_)}, ipackages)
      ;Return loaded epackages
      epackages

  defn read-ipackages (filename:String) -> Tuple<IPackage> :
    val forms = read-file(filename)
    val expanded = try : cons(`$begin, parse-syntax[core / #exps!](forms))
                   catch (e:NoMatchException) : throw(ReplErrors(causes(e)))
                   catch (e:Exception) : throw(ReplErrors([e]))
    to-ipackages(expanded, core-imports())

  defn to-ipackage (form) -> IPackage :
    val expanded = try : cons(`$begin, parse-syntax[core / #exps!](form))
                   catch (e:NoMatchException) : throw(ReplErrors(causes(e)))
                   catch (e:Exception) : throw(ReplErrors([e]))
    repl-package(repl-env, to-il(expanded))

  defn ensure-no-duplicate-packages! (packages:Seqable<Symbol>) :
    val pset = HashSet<Symbol>()
    for p in packages do :
      if not add(pset,p) :
        throw(ReplErrors([DuplicatePackage(p)]))

  defn compile-to-el (ipackages:Collection<IPackage>, pkgloader:PkgLoader, repl?:True|False) -> Tuple<EPackage|Pkg> :
    ;First rename the binders
    val renamed = match(rename-il(to-tuple(ipackages))) :
      (renamed:Tuple<IPackage>) : renamed
      (e:RenameErrors) : throw(ReplErrors(errors(e)))

    ;Create pkg environment
    val env = make-pkg-env(pkgloader)

    ;Resolve the binders
    val priority = package-priority(repl-env, name(renamed[0])) when repl?
    val resolved = match(resolve-il(renamed, env, priority)) :
      (e:ResolveErrors) : throw(ReplErrors(errors(e)))
      (e:Tuple<IPackage>) : e

    ;Load all dependencies
    load-dependencies(pkgloader, already-loaded) where :
      val already-loaded = cat(seq(name,resolved), seq(package,packageios(denv)))

    ;Typecheck the program
    val typed = match(type-program(resolved, env)) :
      (e:TProg) : e
      (e:TypeErrors) : throw(ReplErrors(messages(e)))

    ;Compile to EL
    val transient? = repl?
    val epackages = to-el(typed, transient?)
    to-tuple(cat(epackages, pkgs(pkgloader)))    
      
  defn make-pkg-env (pkgloader:PkgLoader) :
    ;Create the table holding the package exports
    ;The exports have their identifiers renamed so that they do not conflict
    ;with fresh-id().
    val package-exports-table = HashTable<Symbol,PackageExports|False>()
    defn package-exports (name:Symbol) :
      within update?(package-exports-table, name) :
        label<PackageExports|False> return :
          defn to-package-exports (io:PackageIO) -> PackageExports :
            defn rename-id (e:Export) : sub-n(e, fresh-id())
            val pexs = /to-package-exports(io)
            sub-exports(pexs, map(rename-id, exports(pexs)))

          ;First look in DEnv
          match(packageio(denv,name)) :
            (io:PackageIO) : return(to-package-exports(io))
            (_:False) : false            
            
          ;Then try loading the pkg
          try :
            val pkg = load-pkg(pkgloader, name)
            return(to-package-exports(packageio(pkg)))
          catch (e:NoPackageException) :
            false

    ;Create the resolution and typing environment
    new stz/resolver/Env & stz/type/Env :
      defmethod stz/resolver/imported-package (this, package:Symbol) :
        package-exports(package)
      defmethod stz/type/exports (this, package:Symbol) :
        package-exports(package) as PackageExports        

  ;Options for exclude-as-live can be:
  ;  `exclude-none:
  ;    Do not exclude any package during liveness check. All
  ;    packages are considered live. Meant to be used for 'update'
  ;    command where the packages are not reevaluated.
  ;  `exclude-all :
  ;    Exclude all packages during liveness check. No package are
  ;    considered live. Meant to be used for 'reload' command where all
  ;    packages are reevaluated.
  ;  `exclude-loaded :
  ;    Exclude the packages that are being reloaded from the liveness
  ;    check. 
  defn load-into-denv (pkgs:Collection<EPackage|Pkg>, exclude-as-live:Symbol) :
    val live-recs = switch(exclude-as-live) :
      `exclude-none : compute-live(vm, [])
      `exclude-all : []
      `exclude-loaded : compute-live(vm, seq(name,pkgs))
    load(denv, to-collection(seq{packageio,pkgs}), live-recs)

  ;Load the following packages, and throw a REPLError upon error.
  defn load-into-denv! (pkgs:Collection<EPackage|Pkg>) :
    match(load-into-denv(pkgs, `exclude-loaded)) :
      (r:LoadErrors) : throw(ReplErrors(errors(r)))
      (r:LoadSuccess) : false

  defn compile-vmpackages (packages:Seqable<EPackage|Pkg>) -> Tuple<VMPackage> :
    to-tuple $ for p in packages seq :
      match(p) :
        (p:EPackage) : compile(lower-unoptimized(p))
        (p:StdPkg) : vmp(p)

  defn run-in-init-order (vmpackages:Seqable<VMPackage>, input:Collection<Symbol>) :
    val load-set = to-hashset<Symbol>(cat(seq(name,vmpackages), input))
    val ordering = initialization-order(package-dependency-graph(denv), input, {not load-set[_]})
    do(init-package{vm, _}, ordering)

  defn rerun-all-packages () :
    ;Retrieve order that all packages were loaded in.
    val loaded-order = to-collection $ seq{package, packageios(denv)}
    val ordering = initialization-order(package-dependency-graph(denv), loaded-order, {false})
    do(init-package{vm, _}, ordering)

  defn intercept-errors (f:() -> ?) :
    try : f()    
    catch (e:IOException) : throw(ReplErrors([e]))
    catch (e:LexerException) : throw(ReplErrors([e]))
    catch (e:CheckErrors) : throw(ReplErrors([e]))
    catch (e:PkgException) : throw(ReplErrors([e]))

  ;============================================================
  ;===================== REPL Structure =======================
  ;============================================================
  new REPL :
    defmethod load-files (this, inputs:Tuple<String|Symbol>) :
      load-files(inputs)
    defmethod update-files (this) :
      update-files()
    defmethod reload (this) :
      reload-files()
    defmethod load-repl (this, form) :
      load-repl(form)
    defmethod clear (this) :
      clear-repl()
    defmethod inside (this, package:Symbol|False) :
      match(package:Symbol) : ensure-package-loaded(package)
      println(inside(repl-env, package))
    defmethod import (this, imp:IImport) :
      ensure-package-loaded(package(imp))
      import(repl-env, imp)
    defmethod unimport (this, package:Symbol) :
      println(unimport(repl-env, package))

;============================================================
;=================== File Environment =======================
;============================================================

deftype FileEnv
defmulti register (e:FileEnv, assoc:FileAssoc) -> False
defmulti changed-files (e:FileEnv) -> Tuple<String>
defmulti source-file? (e:FileEnv, package:Symbol) -> String|False

defstruct FileAssoc :
  filename: String
  timestamp: Long
  packages: Tuple<Symbol>

defn FileEnv () :
  ;Track which source file each package came from
  val source-file-table = HashTable<Symbol,String>()
  val assoc-table = HashTable<String,FileAssoc>()

  new FileEnv :
    defmethod register (this, assoc:FileAssoc) :
      for pkg in packages(assoc) do :
        source-file-table[pkg] = filename(assoc)
      assoc-table[filename(assoc)] = assoc
    defmethod changed-files (this) :
      to-tuple $ for assoc in values(assoc-table) seq? :
        try :
          val file = filename(assoc)
          val old-timestamp = timestamp(assoc)
          val new-timestamp = time-modified(file)
          if new-timestamp != old-timestamp : One(file)
          else : None()
        catch (e:FileStatException) :
          None()
    defmethod source-file? (this, package:Symbol) :
      get?(source-file-table, package)

;============================================================
;=================== Default Imports ========================
;============================================================

defn core-imports () :
  [IImport(`core), IImport(`collections)]

;<doc>=======================================================
;=================== REPL User Environment ==================
;============================================================

# Fundamental State #

  repl-packages: Vector<Symbol>
  imports: HashTable<Symbol,IImport>
  inside-package: Symbol|False
  import-list-table: HashTable<Symbol,ImportList>

All of the REPL packages, in the order that they were loaded, are
stored in repl-packages.

The currently active import statements are stored in imports. 

The package that the REPL is currently "inside" is stored in
inside-package. If the REPL is not currently inside any package, then
false is stored.

# Creating an IPackage from an Expression entered at the REPL #

Input:
  env: ReplEnv
  iexp: IExp
Output:
  ipackage: IPackage

The expanded and checked expression entered at the REPL is given in
iexp. This function creates an IPackage for representing the
expression, with the appropriate imports for the REPL, registers it
with the REPL environment, and returns the IPackage.

If the expression contains any expression that is not appropriate for
the REPL, e.g. defmulti, defmethod, etc. Then a REPLError is thrown.

# Package Priority #

Input:
  env: ReplEnv
  package-name: Symbol
Output:
  priority: Symbol -> Int

Returns a mapping from package names to their priority level. Higher
numbers indicate higher priority.

Definitions from the current repl expression have the highest priority
(INT-MAX), followed by definitions from earlier repl expressions. More
recent repl definitions have higher priority than older repl
definitions. Finally, all other packages have equal prioriity. 

# Private Use: Registering the imports of a source package #

  register (env:ReplEnv, importlist:ImportList) -> False

For every source package that is successfully loaded, its importlist
must be registered with the repl environment. This is used to
construct the import list for when a user goes inside a package. We
wish for the repl environment to inherit the import environment of
that package.

# Private Use: Registering the imports of a source package #

  register-repl (env:ReplEnv, package:Symbol) -> False

Used to register that a REPL package has been successfully loaded
without errors. This is necessary as future REPL expressions need to
import the definitions from the previously-loaded REPL expressions.

# Clearing the REPL Environment #

  clear (repl:REPLEnv)

Clears the list of imported REPL packages. Current imports,
inside-package, and import-list-table are left untouched.

# Importing a package #

  import (repl:REPLEnv, import:IImport) -> False

Either adds or overrides an import of a given package to the REPL
environment so that REPL expressions are able to refer to definitions
in these packages.

# Unimporting a package #

  unimport (repl:REPLEnv, package:Symbol) -> UnimportResult

Removes a package from the import list. 

;============================================================
;=======================================================<doc>

deftype REPLEnv

;For constructing the IPackage
defmulti repl-package (e:REPLEnv, exp:IExp) -> IPackage
defmulti package-priority (e:REPLEnv, package:Symbol) -> Symbol -> Int
defmulti register (env:REPLEnv, importlist:ImportList) -> False
defmulti register-repl (env:REPLEnv, package:Symbol) -> False
defmulti repl-packages (env:REPLEnv) -> Collection<Symbol>
defmulti clear (e:REPLEnv) -> False
defmulti import (e:REPLEnv, import:IImport) -> False
defmulti unimport (e:REPLEnv, package:Symbol) -> UnimportResult
defmulti inside (e:REPLEnv, package:Symbol|False) -> InsideResult

defn REPLEnv () :
  ;Fundamental State
  val previous-packages = Vector<Symbol>()
  val repl-imports = HashTable<Symbol, IImport>()
  var inside-package:Symbol|False = false
  val import-list-table = HashTable<Symbol,ImportList>()

  ;Initialize imports
  defn init-imports () :
    clear(repl-imports)
    for i in core-imports() do :
      repl-imports[package(i)] = i
  init-imports()

  ;Construct IPackage
  defn make-repl-package (exp:IExp) :
    ;First ensure legitimacy
    ensure-repl-expression(exp)

    ;Create the appropriate import list
    val exp-imports = HashTable<Symbol,IImport>()
    defn add-import (i:IImport) : exp-imports[package(i)] = i
    
    ;Previous REPL packages are imported (with visibility into their
    ;private definitions.)
    for p in previous-packages do :
      add-import(IImport(p, [], false, true))
    ;If we are inside a package, then inherit all of the imports from
    ;that package, and import the package itself (with visibility into private bindings).
    match(inside-package) :
      (p:Symbol) :
        ;If we have its imports, then add those imports.
        ;Packages loaded as .pkgs don't have imports.
        if key?(import-list-table, p) :
          do(add-import, imports(import-list-table[p]))
        add-import(IImport(p, [], false, true))
      (p:False) : false
    ;If we have custom imports from user interaction, then import those.
    do(add-import, values(repl-imports))

    ;Create IPackage
    IPackage(gensym(`repl),
             to-tuple(values(exp-imports)),
             List(exp),
             info(exp))

  ;Construct environment object
  new REPLEnv :
    defmethod repl-package (this, exp:IExp) :
      make-repl-package(exp)
    defmethod package-priority (this, package:Symbol) :
      val package-index = to-hashtable<Symbol,Int>(previous-packages, 0 to false)
      fn (name:Symbol) :
        if name == package : INT-MAX
        else if key?(package-index, name) : package-index[name] + 1
        else : 0
    defmethod register (this, il:ImportList) :
      import-list-table[package(il)] = il
    defmethod register-repl (this, package:Symbol) :
      add(previous-packages, package)
    defmethod repl-packages (this) :
      previous-packages
    defmethod clear (this) :
      clear(previous-packages)
    defmethod import (this, import:IImport) :
      repl-imports[package(import)] = import
    defmethod inside (this, package:Symbol|False) :
      if inside-package == package :
        match(package:Symbol) : AlreadyInside(package)
        else : AlreadyOutside()
      else :
        val result = match(inside-package, package) :
          (p0:Symbol, p1:Symbol) : ChangedInside(p0, p1)
          (p0:False, p1:Symbol) : WentInside(p1)
          (p0:Symbol, p1:False) : WentOutside(p0)          
        if package is False : init-imports()
        else : clear(repl-imports)
        inside-package = package
        result
    defmethod unimport (this, package:Symbol) :
      if key?(repl-imports, package) :
        remove(repl-imports, package)
        PackageUnimported(package)
      else :
        PackageNotImported(package)

defn ensure-repl-expression (expression:IExp) :
  ;Detect illegal expressions
  val errors = Vector<Exception>()
  defn not-allowed! (e:IExp) :
    val type-str = match(e) :
      (e:IDefPackage) : "defpackage"
      (e:IDefType) : "deftype"
      (e:IDefmulti) : "defmulti"
      (e:IDefmethod) : "defmethod"
      (e:ILSDefType) : "lostanza deftype"
      (e:ILSDefmethod) : "lostanza defmethod"
      (e:ILSExternFn) : "externfn"
    add(errors, InvalidExp(type-str))

  ;Sweep through expressions and detect invalid expressions
  let loop (e:IExp = expression) :
    match(e) :
      (e:IBegin) : do(loop, exps(e))
      (e:IVisibility) : loop(exp(e))
      (e:IDefPackage|IDefType|IDefmulti|IDefmethod|
         ILSDefType|ILSDefmethod|ILSExternFn) : not-allowed!(e)
      (e) : false

  ;Throw errors if there are any
  throw(ReplErrors(errors)) when not empty?(errors)

defstruct ImportList :
  package: Symbol
  imports: Tuple<IImport>
  
defn import-list (p:IPackage) :
  ImportList(name(p), imports(p))

deftype UnimportResult
defstruct PackageNotImported <: UnimportResult : (package:Symbol)
defstruct PackageUnimported <: UnimportResult : (package:Symbol)

defmethod print (o:OutputStream, r:UnimportResult) :
  print{o, _} $ match(r) :
    (r:PackageNotImported) : "Package %~ has not been imported." % [package(r)]
    (r:PackageUnimported) : "Unimported package %~." % [package(r)]

deftype InsideResult
defstruct AlreadyInside <: InsideResult : (package:Symbol)
defstruct AlreadyOutside <: InsideResult
defstruct ChangedInside <: InsideResult : (old-package:Symbol, new-package:Symbol)
defstruct WentInside <: InsideResult : (package:Symbol)
defstruct WentOutside <: InsideResult : (package:Symbol)

defmethod print (o:OutputStream, r:InsideResult) :
  print{o, _} $ match(r) :
    (r:AlreadyInside) : "REPL environment is already inside package %~." % [package(r)]
    (r:AlreadyOutside) : "REPL environment is not inside any package."
    (r:ChangedInside) : "REPL environment left package %~ and is now inside package %~." % [old-package(r), new-package(r)]
    (r:WentInside) : "REPL environment is now inside package %~." % [package(r)]
    (r:WentOutside) : "REPL environment left package %~." % [package(r)]

;============================================================
;==================== Categorization ========================
;============================================================

;Classifies the incoming files into four types:
;  myfile.stanza
;  myfile.pkg
;  mypackage
;
;Returns either:
;  `package
;  `stanza-file
;  `pkg-file

defn category (name:String|Symbol) -> Symbol :
  match(name) :
    (name:Symbol) :
      `package
    (name:String) :
      switch suffix?{name, _} :
        ".stanza" : `stanza-file
        ".pkg" : `pkg-file
        else : throw(ReplErrors([InvalidExtension(name)]))

;============================================================
;======================== Errors ============================
;============================================================

public defstruct ReplErrors <: Exception :
  messages: Collection

defn DuplicatePackage (name:Symbol) :
  Exception $ "Attempt to load package %~ multiple times." % [name]

defn NoChangedFiles () :
  Exception $ "No changes detected in any loaded file."

defn InvalidExtension (file:String) :
  Exception $ "File %~ does not have a valid Stanza file extension." % [file]

defn InvalidExp (exp-type:String) :
  Exception $ "The interactive REPL does not support %_ statements." % [exp-type]

defn LoadMultipleScripts (script:String) :
  Exception $ "Invalid attempt to load script %~ alongside other files. Scripts must be loaded by themselves." % [script]

;============================================================
;======================== REPL Loop =========================
;============================================================

defn script? (exp:Load) -> String|False :
  defn script? (s:String|Symbol) :
    match(s:String) : suffix?(s, ".repl")
  val s = find(script?, inputs(exp))
  match(s:String) :
    if length(inputs(exp)) > 1 :
      throw(ReplErrors([LoadMultipleScripts(s)]))
    s

public defn eval-exp (repl:REPL, exp:RExp) :
  match(exp) :
    (exp:Load) :
      match(script?(exp)) :
        (file:String) : run-script(repl, file)
        (_:False) : load-files(repl, inputs(exp))
    (exp:Eval) : load-repl(repl, form(exp))
    (exp:Import) : import(repl, import(exp))
    (exp:Unimport) : unimport(repl, package(exp))
    (exp:Inside) : inside(repl, package(exp))
    (exp:Update) : update-files(repl)
    (exp:Reload) : reload(repl)
    (exp:Clear) : clear(repl)

defn run-script (repl:REPL, s:String) :
  try :
    val file = FileInputStream(s)
    try :
      let loop () :
        val exp = read-repl-exp(file)
        match(exp:RExp) :
          eval-exp(repl, exp)
          loop()
    finally :
      close(file)
  catch (e:IOException) :
    throw(ReplErrors([e]))    

public defn repl (args:Tuple<String>) :
  val repl = REPL()

  defn load-initial-files () -> True|False :
    try :
      eval-exp(repl, make-load-exp(args))
      true
    catch (e:ReplErrors) :
      do(println, messages(e))
      false

  defn* repl-loop () :
    val loop? = 
      try :
        val exp = read-repl-exp()
        match(exp:RExp) :
          eval-exp(repl, exp)
          true
      catch (e:ReplErrors) :
        do(println, messages(e))
        true
    repl-loop() when loop?

  ;Launch!
  repl-loop() when load-initial-files()

public defn repl () :
  repl([])

;============================================================
;==================== Run Immediately =======================
;============================================================

public defn run-in-repl (args:Tuple<String>) :
  val repl = REPL()
  try :
    eval-exp(repl, make-load-exp(args))
  catch (e:ReplErrors) :
    do(println, messages(e))

defn make-load-exp (args:Tuple<String>) :
  Load $ for a in args map :
    if index-of-char(a, '.') is Int : a
    else : to-symbol(a)