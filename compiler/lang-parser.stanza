defpackage parser-lang :
   import core
   import reader
   import collections
   import stz/core-macros
   import macro-utils
   import stz/parser2

defsyntax parser :
   defrule :
      sym = (?x) when unwrap-token(x) is Symbol : unwrap-token(x)

      rule = (import ?name:#sym from ?package:#sym) : ImportRule(List(name), package)
      rule = (import (?names:#sym ...) from ?package:#sym) : ImportRule(names, package)
      rule = (public defproduction ?name:#sym : ?type:#type) : DefProductionRule(name, type, true)
      rule = (defproduction ?name:#sym : ?type:#type) : DefProductionRule(name, type, false)
      rule = (public defproduction ?name:#sym) : DefProductionRule(name, `?, true)
      rule = (defproduction ?name:#sym) : DefProductionRule(name, `?, false)
      rule != (defproduction) : fatal("%_: Invalid syntax: %_" % [info, form])

      rule = (defrule ?e:#sym = (?pat ...) : ?body:#exp) : DefActionRule(e, pat, None(), body)
      rule = (defrule ?e:#sym = (?pat ...) when ?guard:#exp : ?body:#exp) : DefActionRule(e, pat, One(guard), body)
      rule = (defrule ?e:#sym != (?pat ...)) : DefNotRule(e, pat, None())
      rule = (defrule ?e:#sym != (?pat ...) when ?guard:#exp) : DefNotRule(e, pat, One(guard))
      rule = (defrule ?e:#sym = inline ?ie:#sym) : DefInlineRule(e, ie)
      rule != (defrule) : fatal("%_: Invalid syntax: %_" % [info, form])
      rule = (fail-if ?e:#sym = (?pat ...) : ?body:#exp) : DefFailRule(e, pat, None(), body)
      rule = (fail-if ?e:#sym = (?pat ...) when ?guard:#exp : ?body:#exp) : DefFailRule(e, pat, One(guard), body)
      rule != (fail-if) : fatal("%_: Invalid syntax: %_" % [info, form])
      rule = (?e:#exp) : StanzaExpressionRule(e)

      prefix-op = (defsyntax ?name:#sym : (?rs:#rule ... ?rest ...)) :
         fatal("Expected rule but got %_." % [rest]) when not empty?(rest)
         compile(DefSyntaxRule(name, rs))

   defrule :
      match-prod = ((?pat ...) : ?body:#exp) : MatchPattern(pat, None(), body)
      match-prod = ((?pat ...) when ?guard:#exp : ?body:#exp) : MatchPattern(pat, One(guard), body)

      prefix-op = (match-syntax(?arg:#exp) : (?ps:#match-prod ... ?rest ...)) :
         fatal("Expected match production but got %_." % [rest]) when not empty?(rest)
         expande(compile(MatchSyntaxRule(arg, ps)))

   defrule :
      prefix-op = (with-syntax(?name0:#sym (+ ?names:#sym) @...) : ?body:#exp) :
         expande(compile-with-syntax(cons(name0,names), body))
      prefix-op = (with-added-syntax(?name0:#sym (+ ?names:#sym) @...) : ?body:#exp) :
         expande(compile-with-added-syntax(cons(name0,names), body))

;============================================================
;================ Compilation Utilities =====================
;============================================================
;
defn expande (x) :
   with-syntax(core) :
      match-syntax(x) :
         (?x:#exp) : x
         
;============================================================
;================= WithSyntax Macro =========================
;============================================================

defn compile-with-syntax (names:List, body) :
   fill-template(`(stz/parser2/call-with-syntax(`names, fn () : body)), [
      `names => names
      `body => body])

defn compile-with-added-syntax (names:List, body) :
   fill-template(`(stz/parser2/call-with-added-syntax(`names, fn () : body)), [
      `names => names
      `body => body])

;============================================================
;====================== Test ================================
;============================================================

defn main () :
   val filename = command-line-arguments()[1]
   with-syntax(parser, core) :
      match-syntax(read-file(filename)) :
         (?es:#exp ...) :
            for e in es do :
               println(e)
               println("\n")

main()
