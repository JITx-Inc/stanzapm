defpackage stz/type-to-kform :
  import core
  import collections
  import stz/namemap
  import stz/ids
  import stz/tl-ir
  import stz/kl-ir
  import stz/tgt-ir
  import stz/pl-ir with :
    prefix(Private, Protected, Public) => P
  import stz/type-calculus
  import stz/primitives
  import stz/utils

public defn to-kform (p:TProg, nm:NameMap) -> List<KPkgComm> :
  ;Using hierarchy and namemap (TODO)
  set-current-hierarchy(hier(p))
  CURRENT-NAMEMAP = nm

  ;Compute declarations
  val all-decls = HashTable<Int,Declare>()
  val package-decls = HashTable<Symbol,List<Declare>>()
  for pkg in packages(p) do :  
    val name = name(pkg as TPackage|TGPackage|KPackage)
    val decls = match(pkg) :
      (pkg:TPackage) : to-declarations(comms(pkg), nm)
      (pkg:TGPackage|KPackage) : declarations(ids(pkg))
    for d in decls do :
      all-decls[n(d)] = d
    package-decls[name] = decls

  ;Compute imports
  defn compute-imports (pkg:Symbol, imports:List<Symbol>) :
    val imported = HashTable<Int,Import>()
    val grouped = HashTable<Symbol,List<Import>>(List())
    defn record-import (pkg:Symbol, i:Import) :
      imported[n(i)] = i
      grouped[pkg] = cons(i, grouped[pkg])    

    defn import (n:Int) :
      if not key?(imported, n) :
        if key?(nm, n) :
          val e = nm[n]
          if package(e) != pkg :
            val id = id(all-decls[n])
            record-import(package(e), Import(n, id))
            do-ids(import, id)
    do({grouped[_] = List()}, imports)
    do(import, used())    

    to-list $ for entry in grouped seq :
      ImportPackage(key(entry), value(entry))

  ;Translate to KPackage
  defn to-kpackage (pkg:TPackage) :
    val comms* = accumulate-used(seq-append{to-comms, comms(pkg)})
    val imports* = compute-imports(name(pkg), imports(pkg))
    val ids = PkgIds(package-decls[name(pkg)], imports*)
    KPackage(name(pkg), ids, comms*, false)

  ;Translate all packages
  for pkg in packages(p) map :
    match(pkg) :
      (pkg:TPackage) : to-kpackage(pkg)
      (pkg:TGPackage|KPackage) : pkg

;============================================================
;====================== Namemap =============================
;============================================================
;TODO: Won't be necessary after labels are included with extern
;definitions.

var CURRENT-NAMEMAP : False|NameMap = false
defn current-namemap () : CURRENT-NAMEMAP as NameMap

;============================================================
;=================== Declarations ===========================
;============================================================

;TODO: Declarations/Imports should be computed by type level IR
;since they are done using HiStanza types anyway.
public defn to-declarations (cs:List<TComm>, nm:NameMap) -> List<Declare> :
  accumulate<Declare> $ fn (emit) :
    defn declare-targ (n:Int) :
      val e = nm[n]
      emit(Declare(n, TArgId(name(e)), info(e)))
    defn declare-targs (ns:Seqable<Int>) :
      do(declare-targ, ns)
    defn declare-field (n:Int) :
      val e = nm[n]
      emit(Declare(n, FieldId(name(e)), info(e)))

    for c in cs do :
      match(c) :
        ;HiStanza Commands
        (c:TDefType) :
          declare-targs(args(c))
          do(declare-targs{args(_)}, children(c))
          val e = nm[n(c)]
          val cs = for child in children(c) map :
            ChildDec(n(child), args(child), parent(child))
          val id = TypeId(name(e), args(c), parent(c), cs, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TDef) :
          val e = nm[n(c)]
          val id = ValId(name(e), type(c) as Type, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TDefVar) :
          val e = nm[n(c)]
          val id = VarId(name(e), type(c) as Type, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TDefTuple) :
          for (n in ns(c), t in types(c)) do :
            val e = nm[n]
            val id = ValId(name(e), t as Type, to-visibility(visibility(e)))
            emit(Declare(n, id, info(e)))
        (c:TDefn) :
          declare-targs(cat(targs(c), cargs(c)))
          val e = nm[n(c)]
          val id = FnId(name(e), tail?(c), targs(c), cargs(c),
                        a1(c) as List<Type>, a2(c) as Type,
                        to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TDefmulti) :
          declare-targs(cat(targs(c), cargs(c)))
          val e = nm[n(c)]
          val id = MultiId(name(e), false, targs(c), cargs(c),
                           a1(c) as List<Type>, a2(c) as Type,
                           to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TDefmethod) :
          false
        (c:TInit) :
          false
          
        ;LoStanza commands
        (c:TLDefType) :
          declare-targs(args(c))
          val e = nm[n(c)]
          val fs = for f in fields(c) map :
            declare-field(n(f))
            FieldDec(n(f), name(f), type(f))
          val id = LSTypeId(name(e), args(c), parent(c), fs, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))          
        (c:TLDef) :
          val e = nm[n(c)]
          val id = LSValId(name(e), type(c) as LSType, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TLDefVar) :
          val e = nm[n(c)]
          val id = LSVarId(name(e), type(c) as LSType, to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TLDefn) :
          declare-targs(cat(targs(c), cargs(c)))
          val e = nm[n(c)]
          val id = LSFnId(name(e), tail?(c), targs(c), cargs(c),
                          a1(c), a2(c),
                          to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TLDefmethod) :
          false
        (c:TLInit) :
          false
          
        ;External commands  
        (c:TLExternFn) :
          val e = nm[n(c)]
          val id = LSExternFnId(name(e), name(e), a1(c), a2(c), to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
        (c:TExtern) :
          val e = nm[n(c)]
          val id = LSExternId(name(e), name(e), type(c), to-visibility(visibility(e)))
          emit(Declare(n(c), id, info(e)))
          
        ;Unknown  
        (c) :
          fatal("Unsupported declaration: %_" % [c])

defn to-visibility (v:Vis) :
  match(v) :
    (v:Private) : PPrivate()
    (v:Protected) : PProtected()
    (v:Public) : PPublic()

;============================================================
;=================== Usages =================================
;============================================================

val USAGE-SET = HashTable<Int,True>()
defn used () : to-vector<Int>(keys(USAGE-SET))
defn record-use (n:Int) : USAGE-SET[n] = true

defn accumulate-used<?T> (f: () -> ?T) -> T :
  clear(USAGE-SET)
  f()

;============================================================
;=================== Forms ==================================
;============================================================

;TODO: Extern labels should be included with the type ir.
defn to-comms (c:TComm) -> List<KComm> :
  wrap-in-list<KComm> $ match(c) :
    ;HiStanza commands
    (c:TDefType) :
      val name = to-string(name(current-namemap()[n(c)]))
      KDefTag(
        n(c),
        name,
        to-tag?(parent(c)),
        map(to-child, children(c)))
    (c:TDef) :
      KDef(
        n(c),
        to-tag!(type(c)),
        to-exp(value(c)),
        info(c))
    (c:TDefVar) :
      KDefVar(
        n(c),
        to-tag!(type(c)),
        to-exp?(value(c)),
        info(c))
    (c:TDefTuple) :
      to-tuple-defs(
        ns(c),
        types(c),
        value(c),
        info(c))
    (c:TDefn) :
      val func = KFn(
        tail?(c),
        append(targs(c), cargs(c))
        map(to-tag!, a1(c))
        to-tag!(a2(c))
        args(c)
        to-exp(body(c)),
        info(c))
      KDefn(n(c), func)
    (c:TDefmulti) :
      KDefmulti(
        n(c),
        false,
        append(targs(c), cargs(c)),
        map(to-tag!, a1(c)),
        to-tag!(a2(c)),
        info(c))
    (c:TDefmethod) :
      val func = KFn(
        tail?(c),
        append(targs(c), cargs(c)),
        map(to-tag!, a1(c)),
        to-tag!(a2(c)),
        args(c),
        to-exp(body(c)),
        info(c))
      val [multi, targs] = to-multi(multi(c))
      record-use(multi)
      KDefmethod(
        multi,
        targs,
        thisn(c),
        func)
    (c:TInit) :
      KInit(to-exp(exp(c)))
      
    ;LoStanza commands
    (c:TLDefType) :
      val p = to-tag?(parent(c))
      val [fs, rf] = to-fields(fields(c))
      val name = to-string(name(current-namemap()[n(c)]))
      KLSDefStruct(n(c), name, p, fs, rf)        
    (c:TLDef) :
      KLSDef(
        n(c),
        to-ls-type(type(c)),
        to-ls-exp-upcast(value(c), type(c)))
    (c:TLDefVar) :
      KLSDefVar(
        n(c),
        to-ls-type(type(c)),
        to-ls-exp-upcast?(value(c), type(c)))
    (c:TLDefn) :
      val func = KLSFn(
        tail?(c),
        append(targs(c), cargs(c)),
        map(to-ls-type!, a1(c)),
        to-ls-type!(a2(c)),
        args(c),
        to-ls-comm(body(c), a2(c)),
        info(c))
      KLSDefn(n(c), func)
    (c:TLDefmethod) :
      val func = KLSFn(
        tail?(c),
        append(targs(c), cargs(c)),
        map(to-ls-type!, a1(c)),
        to-ls-type!(a2(c)),
        args(c),
        to-ls-comm(body(c), a2(c)),
        info(c))
      val [multi, targs] = to-multi(multi(c)) 
      KLSDefmethod(multi, targs, func)
    (c:TLInit) :
      KLSInit(to-ls-comm(comm(c), false))

    (c:TLExternFn) :
      val e = current-namemap()[n(c)]
      val func = KLSFn(
        false,
        List(),
        map(to-ls-type!, a1(c)),
        to-ls-type!(a2(c)),
        args(c),
        to-ls-comm(body(c), a2(c)),
        info(c))
      KLSExternFn(n(c), name(e), func)
    (c:TExtern) :
      val e = current-namemap()[n(c)]
      KLSExtern(n(c), name(e), to-ls-type(type(c)))
      
    ;Unknown
    (c) :
      fatal("Unsupported command: %_" % [c])

defn to-exp (e:TExp) -> KExp :
  match(e) :
    (e:TLet) :
      val def = KDef(
        n(e),
        to-tag!(ntype(e)),
        to-exp(value(e)),
        info(e))
      val body = to-exp(body(e))
      KLet(def, body)
    (e:TLetVar) :
      val def = KDefVar(
        n(e),
        to-tag!(ntype(e)),
        to-exp?(value(e)),
        info(e))
      val body = to-exp(body(e))
      KLet(def, body)
    (e:TLetTuple) :
      KLet(
        to-tuple-defs(ns(e), ntypes(e), value(e), info(e))
        to-exp(body(e)))
    (e:TLetRec) :
      KLetRec(
        seq-append(to-comms, defns(e)) as List<KDefn>,
        to-exp(body(e)))
    (e:TSeq) :
      KSeq(to-exp(a(e)), to-exp(b(e)))
    (e:TMatch) :
      KMatch(map(to-exp, args(e)),
             map(to-branch, branches(e)),
             info(e))
    (e:TFn) :
      KAFn(to-fn(e))
    (e:TMulti) :
      KAFn(KMultifn(map(to-fn, funcs(e))))
    (e:TNew) :
      val tag = to-tag(class(e))
      KNew(
        tag,
        object-name(tag),
        seq-append(to-comms, methods(e)) as List<KDefmethod>,
        info(e))
    (e:TRef) :
      record-use(n(e))
      KVar(n(e), info(e))
    (e:TCast|TUpCast) :
      KCast(to-exp(exp(e)),
            to-tag(targ(e)),
            CAST-ERROR-TYPE,
            info(e))
    (e:TSet) :
      record-use(n(ref(e)))
      KSet(n(ref(e)),
           to-tag!(type(ref(e))),
           to-exp(value(e)))
    (e:TDo) :
      val a1 = map(to-tag!{type(ctxt(_) as CExp)}, args(e))
      val a2 = to-tag!(type(e))
      val f = to-exp(func(e))
      val args = map(to-exp, args(e))
      KDo(f, args, a1, a2, info(e))
    (e:TLiteral) :
      KLiteral(value(e))
    (e:TTupleExp) :
      KTuple(map(to-exp, exps(e)), info(e))
    (e:TMix) :
      val exps* = select-all(sel(e) as SSel, exps(e))
      if length(exps*) == 1 : to-exp(head(exps*))
      else : KMix(map({to-exp(_) as KVar|KCurry},exps*))
    (e:TCurry) :
      record-use(n(ref(e)))
      val targs = map(to-tag!, append(targs(e), cargs(e)))
      KCurry(n(ref(e)), targs)
    (e:TPrim) :
      KPrim(op(e), map(to-exp, args(e)))
    (e) :
      fatal("Unsupported Expression: %_" % [e])

defn to-tag (t:Type) -> KTag :
  do-ids(record-use, t)
  to-ktag(t)

defn to-tuple-defs (ns:List<Int>, ntypes:List<False|Type>, value:TExp, info:False|FileInfo) :
  ;Define the tuple
  val value* = KEnsureLength(to-exp(value), length(ntypes), info)
  val def1 = KDef(
               fresh-id(),
               KOf(TUPLE-TYPE-ID),
               value*,
               info)
  ;Destructure the tuple
  val def2 = let :
    val tuple = KVar(n(def1))
    for (n in ns, t in ntypes, i in 0 to false) map :
      KDef(n, to-tag!(t), KTupleGet(tuple, i), info)
  ;Translate let
  cons(def1, def2)

defn to-branch (e:TBranch) :
  KBranch(args(e), map(to-tag!, atypes(e)), to-exp(body(e)), info(e))

defn to-multi (e:TExp) -> [Int, List<KTag>] :
  match(e) :
    (e:TRef) : [n(e), List()]
    (e:TCurry) : [n(ref(e)), map(to-tag!, append(targs(e), cargs(e)))]
    (e:TMix) : to-multi(select-1(sel(e) as SSel, exps(e)))

defn to-child (c:Child) :
  record-use(n(c))
  KChild(n(c),
         to-tag(parent(c)))

defn to-fn (e:TFn) :
  KFn(tail?(e),
      List(),
      map(to-tag!, a1(e)),
      to-tag!(a2(e)),
      args(e),
      to-exp(body(e)),
      info(e))

defn to-tag? (t:False|Type) : to-tag!(t) when t is Type
defn to-tag! (t:False|Type) : to-tag(t as Type)
defn to-exp? (e:False|TExp) : to-exp(e as TExp) when e is TExp

;============================================================
;================= LoStanza Forms ===========================
;============================================================

defn to-ls-comm (c:LSComm, ret:False|LSType) -> KLSComm :
  ;Track block argument types
  val block-types = HashTable<Int,List<LSType>>()
  
  defn translate (c:LSComm) -> KLSComm :
    match(c) :
      (c:LSCall) :
        KLSExec(to-ls-exp(exp(c)))
      (c:LSSet) :
        val et = type(exp(c)) as LSType
        KLSSet(
          to-loc(exp(c))
          to-ls-type(et)
          to-ls-exp-upcast(value(c), et))
      (c:LSLabels) :
        for b in blocks(c) do :
          block-types[n(b)] = atypes(b)
        KLSLabels $ for b in blocks(c) map :
          val types* = map(to-ls-type, atypes(b))
          val body* = translate(body(b))
          KLSLabeledBlock(n(b), args(b), types*, body*)
      (c:LSGoto) :
        val args* = to-ls-exp-upcast(args(c), block-types[n(c)])
        KLSGoto(n(c), args*)
      (c:LSReturn) :
        KLSReturn(to-ls-exp-upcast(exp(c), ret as LSType))
      (c:LSDef) :
        val dt = type(c) as LSType
        KLSDefLocal(n(c), to-ls-type(dt), to-ls-exp-upcast(value(c), dt))
      (c:LSDefVar) :
        val dt = type(c) as LSType
        val dt* = to-ls-type(dt)
        match(value(c)) :
          (v:LSExp) : KLSDefLocalVar(n(c), dt*, to-ls-exp-upcast(v, dt))
          (v:False) : KLSDefLocalVar(n(c), dt*, false)
      (c:LSSeq) :
        KLSSeq(translate(a(c)), translate(b(c)))        
      (c:LSIf) :
        KLSIf(to-ls-exp-upcast(pred(c), LongT()),
              translate(conseq(c)),
              translate(alt(c)))
      (c:LSMatch) :
        val branches* = for b in branches(c) map :
          val ts = map(type{_ as RefT}, atypes(b))
          KLSBranch(args(b),
                    map(to-tag, ts),
                    translate(body(b)))
        KLSMatch(map(to-ls-exp, args(c)), branches*, info(c))            
      (c:LSSkip) :
        KLSSkip()

  translate(c)

;TODO: LSFn shouldn't be using HiStanza expressions.
defn to-ls-ref (e:TExp) -> KLSExp :
  match(e) :
    (e:TRef) :
      record-use(n(e))
      KLSVar(n(e))
    (e:TMix) :      
      val exps* = select-all(sel(e) as SSel, exps(e))
      if length(exps*) == 1 : to-ls-ref(head(exps*))
      else : KLSMix(map({to-ls-ref(_) as KLSVar|KLSCurry},exps*))      
    (e:TCurry) :
      record-use(n(ref(e)))
      val targs = map(to-tag!, append(targs(e), cargs(e)))
      KLSCurry(n(ref(e)), targs)

defn to-ls-exp (e:LSExp) -> KLSExp :
  match(e) :
    (e:LSVar) :
      record-use(n(e))
      KLSVar(n(e))
    (e:LSNew) :
      record-use(n(e))
      val [fts, rft] = field-rest-types(n(e))
      match(rft:LSType) :
        KLSArray(n(e), to-ls-exp-upcast(args(e), cat(fts, repeat(rft))), info(e))
      else :    
        KLSRef(n(e), to-ls-exp-upcast(args(e), fts), info(e))
    (e:LSStruct) :
      record-use(n(e))
      val args* = to-ls-exp-upcast(args(e), field-types!(n(e), TGradual()))
      KLSStruct(n(e), args*)      
    (e:LSAddr) :
      KLSPtr(to-loc(exp(e)))
    (e:LSDeref|LSSlot|LSField) :
      KLSRead(to-loc(e), to-ls-type(type(e) as LSType))
    (e:LSDo) :
      val f* = to-ls-exp(func(e))
      val ft = type(func(e)) as FnT
      val [a1, a2] = arg-types(args(e), ft)
      val args* = to-ls-args(args(e), ft)
      KLSDo(f*, args*, map(to-ls-type,a1), to-ls-type(a2), info(e))
    (e:LSCallC) :
      val f* = to-ls-exp(func(e))
      val ft = type(func(e)) as FnT
      val args* = to-ls-args(args(e), ft)
      val num-std-args = length(a(ft))
      val std-args = headn(args*, num-std-args)
      val var-args = tailn(args*, num-std-args)
      KLSDoC(f*, std-args, var-args, to-ls-type!(type(e)))      
    (e:LSCast) :
      KLSCast(to-ls-exp(exp(e)), to-ls-type(targ(e)))
    (e:LSLiteral) :
      KLSLiteral(value(e))
    (e:LSAnd) :
      KLSAnd(to-ls-exp-upcast(a(e), LongT()),
             to-ls-exp-upcast(b(e), LongT()))
    (e:LSOr) :
      KLSOr(to-ls-exp-upcast(a(e), LongT()),
            to-ls-exp-upcast(b(e), LongT()))
    (e:LSMix) :
      val exps* = select-all(sel(e) as SSel, exps(e))
      if length(exps*) == 1 : to-ls-exp(head(exps*))
      else : KLSMix(map({to-ls-exp(_) as KLSVar|KLSCurry}, exps*))
    (e:LSCurry) :
      record-use(n(ref(e)))
      val targs = append(targs(e), cargs(e))
      KLSCurry(n(ref(e)), map(to-tag!, targs))
    (e:LSFn) :
      to-ls-ref(ref(e))
    (e:LSSizeof) :
      KLSSizeof(to-ls-type(targ(e)))
    (e:LSTagof) :
      record-use(n(e))
      KLSTagof(n(e))
    (e:LSPrim) :
      defn args-any () :
        KLSPrim(op(e), map(to-ls-exp, args(e)))
      defn args-max () :
        defn max-t (x:LSType, y:LSType) :
          y when implicit-upcast?(x, y) else x
        val t = reduce(max-t, seq({type(_) as LSType}, args(e)))
        val args* = map(to-ls-exp-upcast{_, t}, args(e))
        KLSPrim(op(e), args*)
      defn args-ptr-or-max () :
        if any?({type(_) is PtrT|RefT}, args(e)) :
          val args* = for a in args(e) map :
            match(type(a):PtrT|RefT) : to-ls-exp(a)
            else : to-ls-exp-upcast(a, LongT())
          KLSPrim(op(e), args*)
        else :
          args-max()
      defn args-cmp () :
        val e* = args-max()
        match(type(head(args(e))):ByteT|PtrT) :
          KLSPrim(unsigned(op(e)), args(e*))
        else : e*
      defn unsigned (op:LSPrimitive) :
        switch(op) :
          LS-LT-OP : LS-ULT-OP
          LS-GT-OP : LS-UGT-OP
          LS-LE-OP : LS-ULE-OP
          LS-GE-OP : LS-UGE-OP
      match(op(e)) :
        (op:GetterPrimitive|AddrPrimitive|SetterPrimitive) :
          args-any()
        (op) :
          switch(op) :
            LS-ADD-OP : args-ptr-or-max()
            LS-SUB-OP : args-ptr-or-max()
            LS-MUL-OP : args-max()
            LS-DIV-OP : args-max()
            LS-MOD-OP : args-max()            
            LS-AND-OP : args-max()
            LS-OR-OP : args-max()
            LS-XOR-OP : args-max()
            LS-SHL-OP : args-max()
            LS-SHR-OP : args-max()
            LS-ASHR-OP : args-max()
            LS-EQ-OP : args-ptr-or-max()
            LS-NE-OP : args-ptr-or-max()
            LS-LT-OP : args-cmp()
            LS-GT-OP : args-cmp()
            LS-LE-OP : args-cmp()
            LS-GE-OP : args-cmp()
            LS-ULT-OP : args-max()
            LS-UGT-OP : args-max()
            LS-ULE-OP : args-max()
            LS-UGE-OP : args-max()
            ;Unary         
            LS-NOT-OP : args-any()
            LS-NEG-OP : args-any()
            LS-BITS-OP : args-any()
            LS-FNUM-OP : args-any()
            LS-YIELD-OP : args-any()
            LS-NEW-STACK-OP : args-any()
            LS-LAST-STACK-OP : args-any()
    (e) :
      fatal("Unsupported expression: %_" % [e])

defn to-loc (e:LSExp) -> KLoc :
  match(e) :
    (e:LSVar) :
      KVarLoc(n(e))
    (e:LSDeref) :
      match(type(exp(e))) :
        (et:PtrT) : KDeptr(to-ls-exp(exp(e)))
        (et:RefT) : KDeref(to-ls-exp(exp(e)))
    (e:LSSlot) :
      val ptr = match(type(exp(e))) :
        (t:RestT) : to-loc(exp(e))
        (t:PtrT) : KValue(to-ls-exp(exp(e)))
      KSlot(ptr,
            to-ls-type!(type(e)),
            to-ls-exp-upcast(index(e), LongT()))
    (e:LSField) :
      val base = deref-all(exp(e))
      val n = n(type(base) as StructT)
      record-use(n)
      KField(to-loc(base), field-n(n, name(e)))
    (e) :
      KValue(to-ls-exp(e))

;TODO: Upcasting should be done at type level also
defn to-ls-exp-upcast (e:LSExp, t:LSType) :
  val et = type(e) as LSType
  val e* = to-ls-exp(e)
  if implicit-upcast?(et, t) : KLSCast(e*, to-ls-type(t))
  else : e*

defn to-ls-exp-upcast (es:List<LSExp>, ts:Seqable<LSType>) :
  map(to-ls-exp-upcast, es, ts)

defn to-ls-exp-upcast? (e:False|LSExp, t:LSType) :
  match(e:LSExp) : to-ls-exp-upcast(e, t)

;TODO: Upcasting should be done at type level also
defn arg-types (args:List<LSExp>, t:FnT) -> [List<LSType>, LSType] :
  defn ex (ts:List<LSType>, r:False|LSType) :
    match(r:LSType) : cat(ts, repeat(r))
    else : ts
  val a* = to-list(take-n(length(args), ex(a(t), r(t))))
  [a*, b(t)]

;TODO: Upcasting should be done at type level also
defn to-ls-args (es:List<LSExp>, t:FnT) :
  val [a, b] = arg-types(es, t)
  to-ls-exp-upcast(es, a)

;TODO: Should probably be done during type level
defn* deref-all (e:LSExp) :
  match(type(e)) :
    (et:PtrT) :
      deref-all(LSDeref(e, type(et), false, false, info(e)))
    (et:RefT) :
      val t = type(et) as TOf
      val t* = StructT(n(t), type(t))
      deref-all(LSDeref(e, t*, false, false, info(e)))
    (et) :
      e

defn to-ls-type (t:LSType) -> KLSType :
  do-ids(record-use, t)
  to-ktype(t)

defn to-field (f:Field) :
  KStructField(n(f), to-ls-type(type(f)))

defn to-rfield (f:Field) :
  val t = type(type(f) as RestT)
  KStructField(n(f), to-ls-type(t))

defn to-fields (fs:List<Field>) -> [List<KStructField>, False|KStructField] :
  if empty?(fs) :
    [List(), false]
  else if type(last(fs)) is RestT :
    [map(to-field, but-last(fs)), to-rfield(last(fs))]
  else :
    [map(to-field, fs), false]

defn to-ls-type! (t:False|LSType) :
  to-ls-type(t as LSType)

defn field-rest-types (n:Int) :
  val fs = field-types!(n, TGradual())
  if empty?(fs) :
    [List(), false]
  else :
    match(last(fs)) :
      (f:RestT) : [but-last(fs), type(f)]
      (f) : [fs, false]

defn field-n (sn:Int, fname:Symbol) :
  label<Int> return :
    val e = current-hierarchy()[sn] as LSHEntry
    for f in fields(e) first! :
      if name(f) == fname : One(n(f))
      else : None()

;============================================================
;=================== Object Name ============================
;============================================================

defn object-name (t:KTag) :
  string-join{_, "&"} $
    let loop (t:KTag = t) :
      match(t) :
        (t:KOf) : [current-namemap()[n(t)]]
        (t:KAnd) : cat(loop(a(t)), loop(b(t)))

;============================================================
;=================== Utilities ==============================
;============================================================

defn select-1<?T> (ss:SSel, xs:List<?T>) -> T :
  for (x in xs, s in sels(ss)) find! :
    s is-not False

defn select-all<?T> (ss:SSel, xs:List<?T>) -> List<T> :
  to-list $
  for (x in xs, s in sels(ss)) filter :
    s is-not False
