defpackage stz/el-to-vm :
  import core
  import collections
  import stz/el-ir
  import stz/vm-ir

;============================================================
;======================== Driver ============================
;============================================================

public defn compile (epackage:EPackage) :
  val globals = Vector<VMGlobal>()
  val funcs = Vector<VMFunction>()

  val global-table = GlobalTable(epackage)
  val global-counter = to-seq(0 to false by 8)
  for e in exps(epackage) do :
    match(e) :
      (e:EDefn) :
        add(funcs, compile(n(e), func(e), global-table))
      (e:EDef) :
        add(globals, VMGlobal(n(e), 8, next(global-counter), []))
      (e:EInit) :
        false
      (e:EDefType) :
        false

  ;Return program
  VMProg(
    to-tuple(globals)
    []
    []
    []
    to-tuple(funcs)
    [])

;============================================================
;==================== Global Table ==========================
;============================================================

deftype GlobalTable
defmulti type (t:GlobalTable, n:Int) -> EType
defmulti function? (t:GlobalTable, n:Int) -> True|False

defn GlobalTable (epackage:EPackage) :
  val table = IntTable<ETExp>()
  for e in exps(epackage) do :
    match(e:EDefn|EDef|EDefType) :
      table[n(e)] = e
  new GlobalTable :
    defmethod type (this, n:Int) :
      type(table[n] as EDef)
    defmethod function? (this, n:Int) :
      table[n] is EDefn

;============================================================
;=================== Function Compilation ===================
;============================================================

deftype CVal :
  VMImm <: CVal
defstruct Mem <: CVal :
  address: VMImm
  type: VMType

defn compile (fid:Int, f:EFn, global-table:GlobalTable) :
  ;Defs
  val defs = Vector<VMDef>()
  defn makedef (t:VMType) :
    val n = length(defs)
    add(defs, VMDef(n, t, 0))
    Local(n)

  ;Instructions
  val insbuffer = Vector<VMIns>()
  defn emit (c:VMIns) : add(insbuffer, c)

  ;Define locals
  val immtable = IntTable<VMImm>()
  val typetable = IntTable<VMType>()
  for x in cat(args(f), locals(f)) do :
    val t = to-vm-type(type(x))
    typetable[n(x)] = t
    immtable[n(x)] = makedef(t)

  ;Put a compiled value into a compiled location
  defn put (x:CVal, y:CVal) :
    match(x, y) :
      (x:Local, y:VMImm) :
        emit(SetIns(x,y))
      (x:Local, y:Mem) :
        emit(LoadIns(x, address(y), NumConst(0), 0))
      (x:Mem, y:VMImm) :
        emit(StoreIns(address(x), NumConst(0), 0, y))
      (x:Mem, y:Mem) :
        val tmp = makedef(type(y))
        put(tmp, y)
        put(x, tmp)

  defn store-into-x (f:Local -> False, x:EVar) :
    match(cval(x)) :
      (m:Mem) :
        val tmp = makedef(type(m))
        f(tmp)
        put(m, tmp)
      (m:Local) :
        f(m)

  defn store-into-xs (f:Tuple<Local> -> False, x:EVar|False) :
    match(x:EVar) : store-into-x(f{[_]}, x)
    else : f([])

  defn cval (x:EImm) -> CVal :
    match(x) :
      (x:EConst) :
        NumConst(value(x))
      (x:EVar) :
        if key?(immtable, n(x)) :
          immtable[n(x)]
        else :
          val t = to-vm-type(type(global-table, n(x)))
          Mem(GlobalId(n(x)), t)

  defn imm (x:CVal) :
    match(x:Mem) :
      val tmp = makedef(type(x))
      put(tmp, x)
      tmp
    else :
      x as VMImm
  defn imm (x:EImm) : imm(cval(x))
  defn imms (xs:Tuple<EImm>) : map(imm, xs)

  ;Compile instructions
  for ins in ins(f) do :
    match(ins) :
      (ins:ESet) :
        put(cval(x(ins)), cval(y(ins)))
      (ins:ELabel) :
        emit(LabelIns(n(ins)))
      (ins:EDo) :
        val ys* = imms(ys(ins))
        defn call-global (f:Int) :
          within l = store-into-xs(x(ins)) :
            emit(CallIns(l, GlobalId(f), ys*))
        defn call-imm (f:EImm) :
          within l = store-into-xs(x(ins)) :
            emit(CallIns(l, imm(f), ys*))          
        match(/f(ins)) :
          (f:EVar) :
            if function?(global-table, n(f)) : call-global(n(f))
            else : call-imm(f)
          (f) : call-imm(f)
      (ins:EReturn) :
        emit(ReturnIns([imm(y(ins))]))

  ;Return compiled instructions
  val arglocals = for a in args(f) map :
    immtable[n(a)] as Local
  VMFunc(fid, arglocals, to-tuple(defs), to-tuple(insbuffer))

defn to-vm-type (t:EType) :
  match(t) :
    (t:EByte) : VMByte()
    (t:EInt) : VMInt()
    (t:ELong) : VMLong()
    (t:EFloat) : VMFloat()
    (t:EDouble) : VMDouble()
    (t:EOf) : VMRef()