#use-added-syntax(dot-operator)
defpackage stz/branch-format :
  import core
  import stz/typeset

;============================================================
;====================== Format Definitions ==================
;============================================================

;Represents a type-dependent dispatch format.
;Used by BranchTable.
public deftype BranchFormat <: Hashable&Equalable

;Handles 'match' expressions.
public defstruct MatchFormat <: BranchFormat :
  branches: Tuple<Tuple<TypeSet>>
with: (writer, hashable, equalable)

;Handles generated 'dispatch' expressions from
;function mixing.
public defstruct DispatchFormat <: BranchFormat :
  branches: Tuple<Tuple<TypeSet>>
with: (writer, hashable, equalable)

;Handles 'defmulti' declarations before methods are known yet.
public defstruct MultiFormat <: BranchFormat :
  multi: Int
  num-header-args: Int
  num-args: Int
with: (printer, hashable, equalable)

;Handles 'defmulti' declarations after methods are known.
public defstruct ResolvedMultiFormat <: BranchFormat :
  num-header-args: Int
  methods: Tuple<Method>
with: (printer, hashable, equalable)

;Represents type information of one method.
public defstruct Method :
  fid:Int
  types:Tuple<TypeSet>
with: (printer)
 
