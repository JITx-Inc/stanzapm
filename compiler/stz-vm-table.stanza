defpackage stz/vm-table :
  import core
  import collections
  import stz/vm-ir
  import stz/vm
  import stz/algorithms
  import stz/vm-encoder
  import stz/stable-arrays
  import stz/utils
  import stz/dyn-tree

extern memcpy : (ptr<?>, ptr<?>, long) -> int

;============================================================
;================ Definition of all Tables ==================
;============================================================

public lostanza deftype VMTable :
  ;Initializers
  package-inits:ref<HashTable<Symbol,Int|False>> ;For initializing a package

  ;Branches
  branch-table:ref<BranchTable>
  
  ;Globals
  global-table:ref<Vector<LoadedGlobal|False>>  ;For globals
  global-offsets:ref<StableLongArray>           ;For global offsets
  globals:ref<Buffer>

  ;Classes
  class-table:ref<ClassTable>

  ;Constants
  consts:ref<Buffer>
  consts-data:ref<Buffer>

  ;Data
  data-positions:ref<StableIntArray>
  data:ref<Buffer>

  ;Functions
  function-addresses:ref<StableIntArray>
  instructions:ref<Vector<LinkedIns>>

  ;Bytecode
  bytecode:ref<Buffer>

  ;Externs
  extern-addresses:ref<StableLongArray>

  ;FileInfos
  fileinfos:ref<IntTable<FileInfo>>           

;============================================================
;======================= Initialization =====================
;============================================================

public lostanza defn VMTable (class-table:ref<ClassTable>, branch-table:ref<BranchTable>) -> ref<VMTable> :
  return new VMTable{
    ;Initializers
    HashTable<Symbol,Int|False>(),

    ;Branches
    branch-table,
    
    ;Globals
    Vector<LoadedGlobal|False>(),
    StableLongArray(new Int{32}, new Long{-1L}),
    Buffer(),

    ;Classes
    class-table,

    ;Constants
    Buffer(),
    empty-consts-buffer(),

    ;Data
    StableIntArray(new Int{32}, new Int{-1}),
    Buffer(),

    ;Functions
    StableIntArray(new Int{1024}, new Int{-1}),
    Vector<LinkedIns>(),
    Buffer(),

    ;Externs
    StableLongArray(new Int{32}, new Long{-1L})
    
    ;FileInfos
    IntTable<FileInfo>()}

;============================================================
;================= Class Display Analysis ===================
;============================================================
public deftype ClassTable
public defmulti class? (this, c:Int) -> True|False
public defmulti instanceof? (t:ClassTable, child:Int, parent:TypeSet) -> True|False
public defmulti add (t:ClassTable, cs:Seqable<VMClass>) -> False
public defmulti add (t:ClassTable, l:TreeListener) -> False
public defmulti get (t:ClassTable, c:Int) -> VMClass
public defmulti children (t:ClassTable, c:Int) -> Tuple<Int>

public defn ClassTable () :
  val class-table = IntTable<VMClass>()
  val class-tree = DynTree()

  defn instanceof? (child:Int, t:TypeSet) :
    let loop (t:TypeSet = t) :
      match(t) :
        (t:TopType) : true
        (t:SingleType) : parent?(class-tree, child, type(t))
        (t:AndType) : all?(loop, types(t))
        (t:OrType) : any?(loop, types(t))
    
  new ClassTable :
    defmethod add (this, cs0:Seqable<VMClass>) :
      val cs = to-tuple(cs0)
      defn node-state (c:VMClass) :
        match(c) :
          (c:VMAbstractClass) : SortedNodeState(id(c), parents(c), children(c), false)
          (c) : SortedNodeState(id(c), parents(c), [], true)      
      add(class-tree, seq(node-state, cs))
      for c in cs do :
        class-table[id(c)] = c
    defmethod add (this, l:TreeListener) :
      add(class-tree, l)
    defmethod get (this, c:Int) :
      class-table[c]
    defmethod class? (this, c:Int) :
      key?(class-table, c)
    defmethod instanceof? (this, child:Int, parent:TypeSet) :
      instanceof?(child, parent)
    defmethod children (this, c:Int) :
      all-children(class-tree, c)

public defn subtype? (ct:ClassTable, a:TypeSet, b:TypeSet) :
  match(a) :
    (a:TopType) :
      let loop (t:TypeSet = b) :
        match(t) :
          (t:TopType) : true
          (t:SingleType) : false
          (t:AndType) : all?(loop, types(t))
          (t:OrType) : any?(loop, types(t))
    (a:SingleType) :
      instanceof?(ct, type(a), b)
    (a:AndType) :
      any?(subtype?{ct, _, b}, types(a))
    (a:OrType) :
      all?(subtype?{ct, _, b}, types(a))

public defn dispatch-ordering<?T> (class-table:ClassTable, methods:Tuple<?T>, types:T -> Tuple<TypeSet>) :
  defn submethod? (a:Int, b:Int) :
    for (ta in types(methods[a]), tb in types(methods[b])) all? :
      subtype?(class-table, ta, tb)
  to-tuple $ for item in ambiguity-ordering(length(methods), submethod?) seq :
    match(item) :
      (item:Int) :
        methods[item]
      (item:List<Int>) :
        to-tuple(seq({methods[_]}, item))

;============================================================
;=================== Global Loading =========================
;============================================================

defstruct LoadedGlobal :
  offset: Long
  size: Int
  roots: Tuple<Int>
  package: Symbol
with:
  printer => true

public defn roots (globals:Vector<LoadedGlobal|False>) -> Tuple<Int> :
  roots(globals, [])

public defn global-offset (vmt:VMTable, id:Int) -> Long :
  offset(global-table(vmt)[id] as LoadedGlobal)

public defn roots (globals:Vector<LoadedGlobal|False>, exclude:Seqable<Symbol>) -> Tuple<Int> :
  val exset = to-hashset<Symbol>(exclude)
  to-tuple $
    for g in filter-by<LoadedGlobal>(globals) seq-cat :
      if exset[package(g)] :
        []
      else :
        for r in roots(g) seq :
          to-int(offset(g) / 8L) + r  

public defn load-globals (vmt:VMTable, load-globals:Seqable<VMGlobal>, package:Symbol,
                          keep-existing-globals?:True|False) :
  val loaded? = IntSet()
  val gt = global-table(vmt)
  for g in load-globals do :
    val size* = (size(g) + 7) & -8
    add(loaded?, id(g))
    check-existing-entry() where :    
      defn* check-existing-entry () :
        match(get?(gt, id(g))) :
          (l:LoadedGlobal) :
            if keep-existing-globals? :
              fatal("New global size does not match existing global.") when size(l) != size*
              false ;Do nothing
            else :
              if size(l) <= size* : overwrite-entry(l)
              else : make-new-entry()
          (_:False) :
            make-new-entry()
      defn* overwrite-entry (l:LoadedGlobal) :
        val entry = LoadedGlobal(offset(l), size*, roots(g), package)
        put<LoadedGlobal>(gt, id(g), entry)
        void-roots(offset(l))
      defn* make-new-entry () :
        val offset = alloc-global(vmt, size*)
        val entry = LoadedGlobal(offset, size*, roots(g), package)
        put<LoadedGlobal>(gt, id(g), entry)
        set-global-offset(vmt, id(g), offset)
        void-roots(offset)
      defn* void-roots (offset:Long) :
        for r in roots(g) do :
          make-void(globals(vmt), to-int(offset / 8L) + r)
  ;Remove any unloaded globals        
  for i in 0 to length(gt) do :
    match(gt[i]) :
      (g:LoadedGlobal) :
        if /package(g) == package and not loaded?[i] :
          gt[i] = false
      (g:False) :
        false

lostanza defn set-global-offset (vmt:ref<VMTable>, d:ref<Int>, pos:ref<Long>) -> ref<False> :
  val size = length(vmt.global-offsets).value
  if d.value >= size :
    vmt.global-offsets = enlarge(vmt.global-offsets, new Int{d.value + 1}, new Long{-1L})
  set(vmt.global-offsets, d, pos)
  return false

lostanza defn global-table (vmt:ref<VMTable>) -> ref<Vector<LoadedGlobal|False>> :
  return vmt.global-table
lostanza defn globals (vmt:ref<VMTable>) -> ref<Buffer> :
  return vmt.globals
lostanza defn alloc-global (vmt:ref<VMTable>, size:ref<Int>) -> ref<Long> :
  return new Long{alloc(vmt.globals, size.value)}

lostanza val VOID-MARKER:long = void-marker()
lostanza defn make-void (buffer:ref<Buffer>, i:ref<Int>) -> ref<False> :
  val mem:ptr<long> = buffer.mem
  mem[i.value] = VOID-MARKER
  return false

;============================================================
;===================== Class Loading ========================
;============================================================

public lostanza defn load-classes (vmt:ref<VMTable>, classes:ref<Seqable<VMClass>>) -> ref<False> :
  add(vmt.class-table, classes)
  return false


  ;val class-seq = to-seq(classes)
  ;while empty?(class-seq) == false :
  ;  add(vmt.class-table, next(class-seq))
  ;return false

;============================================================
;===================== Const Loading ========================
;============================================================

public lostanza defn load-consts (vmt:ref<VMTable>, consts:ref<Tuple<VMConst>>) -> ref<False> :
  ;Put into const-positions table
  val nconsts = length(consts).value
  val const-space = nconsts * 8L
  val pos = alloc(vmt.consts, const-space) / 8
  for (var i:int = 0, i < nconsts, i = i + 1) :
    make-void(vmt.consts, new Int{(pos + i) as int})

  ;Compile data
  write-consts(vmt.consts-data, consts)

  return false

;============================================================
;=================== Const Compilation ======================
;============================================================

val BYTE-CONST-TAG = 0
val CHAR-CONST-TAG = 1
val INT-CONST-TAG = 2
val LONG-CONST-TAG = 3
val FLOAT-CONST-TAG = 4
val DOUBLE-CONST-TAG = 5
val TRUE-CONST-TAG = 6
val FALSE-CONST-TAG = 7
val LIST-CONST-TAG = 8
val STRING-CONST-TAG = 9
val SYMBOL-CONST-TAG = 10
val TYPE-CONST-TAG = 11
val FN-CONST-TAG = 12
val INTERNED-CONST-TAG = 13

defn empty-consts-buffer () -> Buffer :
  val buffer = Buffer()
  alloc(buffer, 4)
  set-int(buffer, 0, 0)
  buffer

defn write-consts (buffer:Buffer, consts:Tuple<VMConst>) :
  ;Write to byte buffer
  val bytebuffer = ByteBuffer()
  defn emit (v:VMValue) :
    match(v) :
      (v:Char) :
        put(bytebuffer, CHAR-CONST-TAG)
        put(bytebuffer, v)
      (v:Byte) :
        put(bytebuffer, BYTE-CONST-TAG)
        put(bytebuffer, v)
      (v:Int) :
        put(bytebuffer, INT-CONST-TAG)
        put(bytebuffer, v)
      (v:Long) :
        put(bytebuffer, LONG-CONST-TAG)
        put(bytebuffer, v)
      (v:Float) :
        put(bytebuffer, FLOAT-CONST-TAG)
        put(bytebuffer, v)
      (v:Double) :
        put(bytebuffer, DOUBLE-CONST-TAG)
        put(bytebuffer, v)
      (v:String) :
        put(bytebuffer, STRING-CONST-TAG)
        emit-chars(bytebuffer, v)
      (v:Symbol) :
        put(bytebuffer, SYMBOL-CONST-TAG)
        emit-chars(bytebuffer, to-string(v))
      (v:List) :
        put(bytebuffer, LIST-CONST-TAG)
        put(bytebuffer, length(v))
        do(emit, v)
      (v:True) :
        put(bytebuffer, TRUE-CONST-TAG)
      (v:False) :
        put(bytebuffer, FALSE-CONST-TAG)
      (v:VMTypeObject) :
        put(bytebuffer, TYPE-CONST-TAG)
        put(bytebuffer, to-long(id(id(v))))
      (v:VMClosure) :
        put(bytebuffer, FN-CONST-TAG)
        put(bytebuffer, to-long(id(id(v))))
      (v:VMInternedConst) :
        put(bytebuffer, INTERNED-CONST-TAG)
        put(bytebuffer, id(id(v)))
      (v) :
        fatal("Unrecognized constant: %~" % [v])
  do(emit{value(_)}, consts)

  ;Append byte buffer to buffer
  append!(buffer, bytebuffer)
  
  ;Increment number of constants
  val num-previous-consts = get-int(buffer, 0)
  set-int(buffer, 0, num-previous-consts + length(consts))

defn emit-chars (buffer:ByteBuffer, s:String) :
  val n = length(s)
  put(buffer, n)
  for c in s do :
    put(buffer, c)
  val endn = (n + 3) & -4
  for i in n to endn do :
    put(buffer, 0Y)
  
;============================================================
;=================== Build Fileinfo Table ===================
;============================================================
defn load-fileinfos (vmt:VMTable, infos:Seqable<FileInfoEntry>, offset:Int) :
  val infot = fileinfos(vmt)
  for e in infos do :
    val address = pc(e) + offset
    infot[address] = fileinfo(e)

lostanza defn fileinfos (vmt:ref<VMTable>) -> ref<IntTable<FileInfo>> :
  return vmt.fileinfos

;============================================================
;====================== Data Loading ========================
;============================================================

public lostanza defn load-data (vmt:ref<VMTable>, d:ref<VMData>) -> ref<False> :
  ;Put into data buffer
  val size = length(data(d)).value
  val size* = (size + 7L) & -8L
  val pos = alloc(vmt.data, size*) / 8
  call-c memcpy(addr!(vmt.data.mem[pos]), addr!(data(d).data), size)
  if non-negative?(vmt.data-positions, id(d)) : fatal("Data already defined.")
  set-data-position(vmt, id(d), new Int{pos as int})
  return false

public defn load-datas (vmt:VMTable, ds:Tuple<VMData>) :
  do(load-data{vmt, _}, ds)

lostanza defn set-data-position (vmt:ref<VMTable>, d:ref<Int>, pos:ref<Int>) -> ref<False> :
  val size = length(vmt.data-positions).value
  if d.value >= size :
    vmt.data-positions = enlarge(vmt.data-positions, new Int{d.value + 1}, new Int{-1})
  set(vmt.data-positions, d, pos)
  return false

lostanza defn non-negative? (xs:ref<Array<Int>>, i:ref<Int>) -> long :
  val l = length(xs).value
  if i.value < l :
    if get(xs,i).value >= 0 : return 1L
    else : return 0L
  else :
    return 0L

;============================================================
;====================== Extern Loading ======================
;============================================================

public lostanza defn load-extern (vmt:ref<VMTable>, e:ref<VMExtern>) -> ref<False> :
  val address = get(EXTERN-TABLE, name(e))
  set-extern-address(vmt, id(e), address)
  return false

public defn load-externs (vmt:VMTable, es:Tuple<VMExtern>) :
  do(load-extern{vmt, _}, es)

lostanza defn set-extern-address (vmt:ref<VMTable>, d:ref<Int>, pos:ref<Long>) -> ref<False> :
  val size = length(vmt.extern-addresses).value
  if d.value >= size :
    vmt.extern-addresses = enlarge(vmt.extern-addresses, new Int{d.value + 1}, new Long{-1L})
  set(vmt.extern-addresses, d, pos)
  return false

;============================================================
;===================== Function Loading =====================
;============================================================

public lostanza defn load-function (vmt:ref<VMTable>, id:ref<Int>, lf:ref<LinkedFunction>) -> ref<False> :
  ;Load instructions into instruction vector
  val address = length(vmt.instructions)
  add-all(vmt.instructions, instructions(lf))
  set-function-address(vmt, id, address)

  ;Load fileinfos into fileinfo table
  load-fileinfos(vmt, fileinfos(lf), address)
  
  return false

public lostanza defn load-function (vmt:ref<VMTable>, id:ref<Int>, ef:ref<EncodedFunction>) -> ref<False> :
  ;Load bytecode into bytecode vector
  val num-bytes = length(buffer(ef))
  val offset = alloc(vmt.bytecode, num-bytes.value)
  call-c memcpy(vmt.bytecode.mem + offset, data(buffer(ef)), num-bytes.value)

  ;Put into function table
  val pos = new Int{(offset / 4) as int}
  set-function-address(vmt, id, pos)
  
  ;Load fileinfos into fileinfo table
  load-fileinfos(vmt, fileinfos(ef), pos)
  
  return false

lostanza defn set-function-address (vmt:ref<VMTable>, f:ref<Int>, address:ref<Int>) -> ref<False> :
  val size = length(vmt.function-addresses).value
  if f.value >= size :
    vmt.function-addresses = enlarge(vmt.function-addresses, new Int{f.value + 1}, new Int{-1})
  set(vmt.function-addresses, f, address)
  return false

;============================================================
;=================== IntArray Enlarging =====================
;============================================================
    
defn enlarge (xs:StableIntArray, min-size:Int, default:Int) -> StableIntArray :
  var new-size:Int = length(xs)
  while new-size < min-size : new-size = new-size * 2
  val ys = StableIntArray(new-size)
  block-copy(length(xs), ys, 0, xs, 0)
  for i in length(xs) to length(ys) do :
    ys[i] = default
  ys

defn enlarge (xs:StableLongArray, min-size:Int, default:Long) -> StableLongArray :
  var new-size:Int = length(xs)
  while new-size < min-size : new-size = new-size * 2
  val ys = StableLongArray(new-size)
  block-copy(length(xs), ys, 0, xs, 0)
  for i in length(xs) to length(ys) do :
    ys[i] = default
  ys

;============================================================
;====================== Initializers ========================
;============================================================

public lostanza defn set-init (vmt:ref<VMTable>, package:ref<Symbol>, init:ref<Int|False>) -> ref<False> :
  set(vmt.package-inits, package, init)
  return false

public lostanza defn init (vmt:ref<VMTable>, package:ref<Symbol>) -> ref<Int|False> :
  return get(vmt.package-inits, package)

;============================================================
;========================= Utility ==========================
;============================================================

defn put<T> (xs:Vector<T|False>, k:Int, v:T) :
  if k >= length(xs) :
    lengthen(xs, k + 1, false)
  xs[k] = v
  
defn get?<?T> (xs:Vector<?T>, k:Int) -> T|False :
  if k < length(xs) : xs[k]