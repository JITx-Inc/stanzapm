defpackage stz/auto-doc :
  import core
  import collections
  import stz/namemap
  import stz/compiler
  import stz/front-end
  import stz/visibility
  import stz/il-ir
  import stz/pkg
  import stz/dl-ir
  import stz/algorithms
  import stz/proj-manager

defn public-definitions (ipackage:IPackage) -> Seq<IExp> :
  generate<IExp> :
    defn loop (e:IExp) :
      defn* loop-public (e:IExp) :
        match(e) :
          (e:IBegin) :
            do(loop-public, exps(e))
          (e:IDefn|IDef|IDefChild|IDefType|IDefVar|IDefmulti|IDefmethod|ILSDefn|ILSDefType|ILSDefmethod) :
            yield(e)
          (e) :
            false
      match(e) :
        (e:IBegin) :
          do(loop, exps(e))
        (e:IDoc) :
          yield(e)
        (e:IVisibility) :
          loop-public(exp(e)) when visibility(e) is Public
        (e) :
          false
    for e in exps(ipackage) do :
      loop(e)

defn lookup (nm:NameMap, e:IExp) :
  match(e) :
    (e:VarN) : name(nm[n(e)])
    (e) : e

defn pretty-targs (nm:NameMap, targs:List<IExp>) :
  "" when empty?(targs) else ("<%,>" % [pretties(nm, targs)])

defn pretty (nm:NameMap, e:IExp) :
  match(e) :
    (e:IOf) : "%_%_" % [pretty(nm, class(e)), pretty-targs(nm, args(e))]
    (e:IAnd) : "%_&%_" % [pretty(nm, a(e)), pretty(nm, b(e))]
    (e:IOr) : "%_|%_" % [pretty(nm, a(e)), pretty(nm, b(e))]
    (e:IArrow) : "%_ -> %_" % [pretties(nm, a1(e)), pretty(nm, a2(e))]
    (e:IGradual) : "?"
    (e:IVoid) : "void"
    (e:INone) : "none"
    (e:ITuple) : "[%,]" % [pretties(nm, exps(e))]
    (e:IVar) : name(e)
    (e:ICap) : "?%_" % [pretty(nm, name(e))]
    (e:ILSOfT) : "%_%_" % [pretty(nm, class(e)), pretty-targs(nm, args(e))]
    (e:ILSRefT) : "ref<%_>" % [pretty(nm, type(e))]
    (e:VarN) : lookup(nm, e)
    (e:Raw) : pretty(nm, class(e))
    (e) : e

defn pretties (nm:NameMap, e:List<IExp>) :
  map(pretty{nm, _}, e)

defn pretty-arg-type (nm:NameMap, a:IExp, t:IExp) :
  "%_:%_" % [lookup(nm, a), pretty(nm, t)]

defn pretty-return-type (nm:NameMap, t:IExp) :
  "" when t is INone else (" -> %_" % [pretty(nm, t)])

defn pretty-parent (nm:NameMap, t:IExp) :
  "" when t is INone else (" <: %_" % [pretty(nm, t)])

defn pretty-type (nm:NameMap, t:IExp) :
  "" when t is INone else (" : %_" % [pretty(nm, t)])

defn pretty-args (nm:NameMap, targs:List<IExp>, args:List<IExp>, a1:List<IExp>, a2:IExp) :
  "%_ (%,)%_" % [pretty-targs(nm, targs), map(pretty-arg-type{nm, _, _}, args, a1), pretty-return-type(nm, a2)]

defn pretty-args (nm:NameMap, targs:List<IExp>, a1:List<IExp>, a2:IExp) :
  "%_ (%,)%_" % [pretty-targs(nm, targs), pretties(nm, a1), pretty-return-type(nm, a2)]

defn print-definition (nm:NameMap, e:IExp) :
  match(e) :
    (e:ILSDefType) :   println("lostanza deftype %_%_%_" % [lookup(nm, name(e)) pretty-targs(nm, args(e)) pretty-parent(nm, parent(e))])
    (e:ILSDefn) :      println("lostanza defn %_%_" % [lookup(nm, name(e)) pretty-args(nm, targs(e), args(e), a1(e) a2(e))])
    (e:ILSDefmethod) : println("lostanza defmethod %_%_" % [multi(e) pretty-args(nm, targs(e), args(e), a1(e) a2(e))])
    (e:IDefType) :     println("deftype %_%_%_" % [class(e) pretty-targs(nm, args(e)) pretty-parent(nm, parent(e))])
    (e:IDef) :         println("val %_%_" % [lookup(nm, name(e)) pretty-type(nm, type(e))])
    (e:IDefVar) :      println("var %_%_" % [lookup(nm, name(e)) pretty-type(nm, type(e))])
    (e:IDefn) :        println("defn %_%_" % [lookup(nm, name(e)) pretty-args(nm, targs(e), args(e), a1(e) a2(e))])
    (e:IDefmulti) :    println("defmulti %_%_" % [lookup(nm, name(e)) pretty-args(nm, targs(e), a1(e) a2(e))])
    (e:IDefmethod) :   println("defmethod %_%_" % [lookup(nm, multi(e)) pretty-args(nm, targs(e), args(e), a1(e) a2(e))])
    (e:IDoc) :         println("%_" % [value(string(e) as ILiteral)])
    (e) : false

defn breakup<?T> (break?:T -> True|False, s:Seqable<?T>) -> Seqable<Tuple<T>> :
  generate<Tuple<T>> :
    val ss = to-seq(s)
    val v = Vector<T>()
    let loop () :
      if empty?(ss) :
        yield(to-tuple(v)) when length(v) > 0
      else :
        val e = next(ss)
        if break?(e) :
          yield(to-tuple(v)) when length(v) > 0
          clear(v)
        add(v, e)
        loop()

public defn gen-doc (nm:NameMap, all-ipackages:Seqable<IPackage>) :
  for ipackage in all-ipackages do :
    val definitions = public-definitions(ipackage)
    ;; println(";;; FILENAME %_" % [filename])
    val groups = breakup(fn (d) : d is IDefType|ILSDefType|IPackage, definitions)
    var code? = false
    for group in groups do :
      for def in group do :
        val to-code? = not def is IDoc
        if to-code? == true and code? == false :
          println("```") 
        else if to-code? == false and code? == true :
          println("```") 
        code? = to-code?
        print-definition(nm, def)
      println("```") when code?
      code? = false
    println("")

public defn auto-doc (settings:BuildSettings, output:String|False) :
  println("AUTO DOCING...")
  val dep-result = dependencies(settings, true)
  gen-doc(namemap(dep-result), filter-by<IPackage>(packages(dep-result)))

  ; for package in ipackages(settings) do :
  ;   for e in exps(package) do :
  ;     match(e:IDoc) : println(e)


