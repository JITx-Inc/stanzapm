defpackage stz/tgt-ir :
  import core
  import collections
  import stz/kl-ir
  import stz/khier

;Use utilities for defining ASTs quickly.
#use-added-syntax(stz-ast-lang, stz-printer-lang)

;============================================================
;================== Program Structure =======================
;============================================================

public defstruct TGProg :
  hier: KHierarchy
  decls: Vector<TGDecl>

public deftype TGDecl
public defstruct TGDefn <: TGDecl :
  n: Int|Symbol
  ins: Vector<TGIns>

public defstruct TGDefmulti <: TGDecl :
  n: Int
  tarity: Int
  arity: Int
  methods: List<TGMethod>

public defstruct TGMethod :
  n: Int
  tags: List<KTag>

public defstruct TGDefConst <: TGDecl :
  n: Int
  value : TGConst

public defstruct TGDefObject <: TGDecl :
  n: Int
  size: Int
  types: List<TGType>
  offsets: List<Int>
  rsize: Int
  rtypes: List<TGType>
  roffsets: List<Int>

public defstruct TGTypeObject :
  n: Int

public defstruct TGDefGlobal <: TGDecl :
  n: Int
  size: Int
  alignment: Int
  types: List<TGType>
  offsets: List<Int>

public deftype TGConst :
  Char <: TGConst
  Byte <: TGConst
  Int <: TGConst
  Long <: TGConst
  Float <: TGConst
  Double <: TGConst
  String <: TGConst
  Symbol <: TGConst
  List<T> <: TGConst
  True <: TGConst
  False <: TGConst
  TGTypeObject <: TGConst

;============================================================
;=================== Printers ===============================
;============================================================

defmethod print (o:OutputStream, p:TGProg) :
  print-all(o, join(decls(p), "\n"))

defmethod print (o:OutputStream, c:TGTypeObject) :
  print(o, "TypeObj%_" % [n(c)])

defmethod print (o:OutputStream, c:TGMethod) :
  print(o, "%_ => %_" % [tags(c), n(c)])
  
defmethod print (o:OutputStream, d:TGDecl) :
  match(d) :
    (d:TGDefn) :
      print(o, "defn %_" % [n(d)])
      for e in ins(d) do :
        print(o, "\n  %_" % [e])
    (d:TGDefmulti) :
      print(o, "defmulti %_ (tarity = %_, arity = %_) %," % [
        n(d), tarity(d) arity(d), methods(d)])
    (d:TGDefConst) :
      print(o, "defconst %_ = %_" % [n(d), value(d)])
    (d:TGDefObject) :
      print(o, "defobject %_" % [n(d)])
      print(o, "\n  size = %_" % [size(d)])
      print(o, "\n  types = %_" % [types(d)])
      print(o, "\n  offsets = %_" % [offsets(d)])
      print(o, "\n  rsize = %_" % [rsize(d)])
      print(o, "\n  rtypes = %_" % [rtypes(d)])
      print(o, "\n  roffsets = %_" % [roffsets(d)])
    (d:TGDefGlobal) :
      print(o, "defglobal %_" % [n(d)])
      print(o, "\n  size = %_" % [size(d)])
      print(o, "\n  alignment = %_" % [alignment(d)])
      print(o, "\n  types = %_" % [types(d)])
      print(o, "\n  offsets = %_" % [offsets(d)])

;============================================================
;================== Instructions ============================
;============================================================

public defast :
  ;Immediates
  deftype TGImm
  defnodes TGImm :
    TGVar : (n:Int)
    TGByte : (value:Byte)
    TGInt : (value:Int)
    TGTag : (n:Int, marker?:True|False)
    TGLong : (value:Long)
    TGMem : (n:Int, offset:Int)
    TGExMem : (lbl:Symbol, offset:Int)
    TGRegSP
  
  ;Types
  deftype TGType <: Equalable
  deftype TGIntegerT <: TGType
  deftype TGRealT <: TGType
  defnodes TGIntegerT :
    TGByteT
    TGIntT
    TGLongT
    TGRefT
  defnodes TGRealT :
    TGFloatT
    TGDoubleT

  ;Instructions
  deftype TGIns
  defnodes TGIns :
    TGDef :
      n: Int
      type: TGType
    TGDefData :
      n: Int
      value: ?
    TGArgs :
      xs: List<TGVar>
    TGReturn :
      xs: List<TGImm>
    TGSet :
      x: TGVar
      y: TGImm
    TGConv :
      x: TGVar
      y: TGImm
    TGInterpret :
      x: TGVar
      y: TGImm
    TGBinOp :
      x: TGVar
      op: TGOp
      y: TGImm
      z: TGImm
    TGUnaOp :
      x: TGVar
      op: TGOp
      y: TGImm
    TGLoad :
      x: TGVar
      y: TGImm
      offset: Int
    TGStore :
      x: TGImm
      y: TGImm
      offset: Int
    TGCall :
      arity: Int
      xs: List<TGVar>
      f: TGImm
      ys: List<TGImm>
    TGTCall :
      arity: Int
      f: TGImm
      ys: List<TGImm>
    TGSwitchStack :
      x: TGVar
      s: TGImm
      y: TGImm   
    TGLabel :
      n: Int
    TGGoto :
      n: Int
    TGBinCmp :
      n: Int
      op: TGOp
      x: TGImm
      y: TGImm
    TGUnaCmp :
      n: Int
      op: TGOp
      x: TGImm
    TGNullCmp :
      n: Int
      op: TGOp
    TGMatch :
      xs: List<TGImm>
      branches: List<TGBranch>
    TGDispatch :
      xs: List<TGImm>
      branches: List<TGBranch>
    TGCallC :
      xs: List<TGVar>
      f: TGImm
      ys: List<TGImm>
      vs: List<TGImm>
    TGArgsC :
      xs: List<TGVar>
    TGReturnC :
      xs: List<TGImm>
    TGNewStack
    TGSaveCContext
    TGSetRSP :
      x: TGImm

  ;Operations
  deftype TGOp
  defnodes TGOp :
    TGAddOp
    TGSubOp
    TGMulOp
    TGDivOp
    TGModOp
    TGAndOp
    TGOrOp
    TGXorOp
    TGShlOp
    TGShrOp
    TGAshrOp
    TGEqOp
    TGNeOp
    TGLtOp
    TGGtOp
    TGLeOp
    TGGeOp
    TGUleOp
    TGUltOp
    TGUgtOp
    TGUgeOp
    TGNotOp
    TGNegOp

    TGArityNeOp
    TGHasStackSpace

public defstruct TGBranch :
  tags: List<KTag>
  n: Int

;============================================================
;======================= Type Sizes =========================
;============================================================

public defn size (t:TGType) :
  match(t) :
    (t:TGByteT) : 1
    (t:TGIntT) : 4
    (t:TGLongT) : 8
    (t:TGRefT) : 8
    (t:TGFloatT) : 4
    (t:TGDoubleT) : 8

public defn type (x:TGImm) :
  match(x) :
    (x:TGByte) : TGByteT()
    (x:TGInt) : TGIntT()
    (x:TGTag) : TGLongT()
    (x:TGLong) : TGLongT()
    (x:TGMem) : TGLongT()
    (x:TGExMem) : TGLongT()
    (x:TGRegSP) : TGLongT()

public defn TGIntegerT (size:Int) -> TGIntegerT :
  switch(size) :
    1 : TGByteT()
    4 : TGIntT()
    8 : TGLongT()

public defn TGRealT (size:Int) -> TGRealT :
  switch(size) :
    4 : TGFloatT()
    8 : TGDoubleT()

public defn TGIntegerT (t:TGType) :
  match(t:TGIntegerT) : t
  else : TGIntegerT(size(t))

public defn TGRealT (t:TGType) :
  match(t:TGRealT) : t
  else : TGRealT(size(t))

;============================================================
;===================== Constructors =========================
;============================================================

public defn TGMem (n:Int) :
  TGMem(n, 0)
public defn TGTag (n:Int) :
  TGTag(n, false)

;============================================================
;======================= Type Equality ======================
;============================================================

defmethod equal? (a:TGType, b:TGType) :
  match(a, b) :
    (a:TGByteT, b:TGByteT) : true
    (a:TGIntT, b:TGIntT) : true
    (a:TGLongT, b:TGLongT) : true
    (a:TGRefT, b:TGRefT) : true
    (a:TGFloatT, b:TGFloatT) : true
    (a:TGDoubleT, b:TGDoubleT) : true
    (a, b) : false  

;============================================================
;======================= Printers ===========================
;============================================================

defmethod print (o:OutputStream, i:TGIns) :
  print{o, _} $ match(i) :
    (i:TGDef) : "def %_ : %_" % [n(i), type(i)]
    (i:TGDefData) : "defdata %_ : %_" % [n(i), value(i)]
    (i:TGArgs) : "(%,) = args" % [xs(i)]
    (i:TGReturn) : "return (%,)" % [xs(i)]
    (i:TGSet) : "%_ = %_" % [x(i), y(i)]
    (i:TGConv) : "%_ = conv %_" % [x(i), y(i)]
    (i:TGInterpret) : "%_ = interpret %_" % [x(i), y(i)]
    (i:TGBinOp) : "%_ = %_(%_, %_)" % [x(i), op(i), y(i), z(i)]
    (i:TGUnaOp) : "%_ = %_(%_)" % [x(i), op(i), y(i)]
    (i:TGLoad) : "%_ = [%_ + %_]" % [x(i), y(i), offset(i)]
    (i:TGStore) : "[%_ + %_] = %_" % [x(i), offset(i), y(i)]
    (i:TGCall) : "(%,) = call/%_ %_(%,)" % [xs(i), arity(i), f(i), ys(i)]
    (i:TGCallC) : "(%,) = call %_(%, | %,)" % [xs(i), f(i), ys(i), vs(i)]
    (i:TGTCall) : "tcall/%_ %_(%,)" % [arity(i), f(i), ys(i)]
    (i:TGSwitchStack) : "%_ = switch-stack(%_, %_)" % [x(i), s(i), y(i)]
    (i:TGNewStack) : "new-stack"
    (i:TGSaveCContext) : "save-c-context"
    (i:TGSetRSP) : "RSP = %_" % [x(i)]
    (i:TGLabel) : "label %_" % [n(i)]
    (i:TGGoto) : "goto %_" % [n(i)]
    (i:TGBinCmp) : "goto %_ when %_(%_, %_)" % [n(i), op(i), x(i), y(i)]
    (i:TGUnaCmp) : "goto %_ when %_(%_)" % [n(i), op(i), x(i)]
    (i:TGNullCmp) : "goto %_ when %_" % [n(i), op(i)]
    (i:TGMatch) : "match(%,): %@" % [xs(i), branches(i)]
    (i:TGDispatch) : "dispatch(%,): %@" % [xs(i), branches(i)]
    (i:TGArgsC) : "(%,) = args-c" % [xs(i)]
    (i:TGReturnC) : "return-c (%,)" % [xs(i)]

defmethod print (o:OutputStream, b:TGBranch) :
  print(o, "%_ => %_" % [tags(b), n(b)])

defmethod print (o:OutputStream, t:TGType) :
  print{o, _} $ match(t) :
    (t:TGByteT) : "Byte"
    (t:TGIntT) : "Int"
    (t:TGLongT) : "Long"
    (t:TGRefT) : "Ref"
    (t:TGFloatT) : "Float"
    (t:TGDoubleT) : "Double"

defmethod print (o:OutputStream, x:TGImm) :
  print{o, _} $ match(x) :
    (x:TGByte) : value(x)
    (x:TGInt) : value(x)
    (x:TGLong) : value(x)
    (x:TGMem) :
      if offset(x) == 0 : "M%_" % [n(x)]
      else : "(M%_ + %_)" % [n(x), offset(x)]
    (x:TGExMem) :
      if offset(x) == 0 : "M%_" % [lbl(x)]
      else : "(M%_ + %_)" % [lbl(x), offset(x)]
    (x:TGVar) : "V%_" % [n(x)]
    (x:TGTag) : "T%_" % [n(x)]
    (x:TGRegSP) : "RSP"
    
defmethod print (o:OutputStream, x:TGOp) :
  print{o, _} $ match(x) :
    (x:TGAddOp) : "add"
    (x:TGSubOp) : "sub"
    (x:TGMulOp) : "mul"
    (x:TGDivOp) : "div"
    (x:TGModOp) : "mod"
    (x:TGAndOp) : "and"
    (x:TGOrOp) : "or"
    (x:TGXorOp) : "xor"
    (x:TGShlOp) : "shl"
    (x:TGShrOp) : "shr"
    (x:TGAshrOp) : "ashr"
    (x:TGEqOp) : "eq"
    (x:TGNeOp) : "ne"
    (x:TGLtOp) : "lt"
    (x:TGGtOp) : "gt"
    (x:TGLeOp) : "le"
    (x:TGGeOp) : "ge"
    (x:TGUleOp) : "ule"
    (x:TGUltOp) : "ult"
    (x:TGUgtOp) : "ugt"
    (x:TGUgeOp) : "uge"
    (x:TGNotOp) : "not"
    (x:TGNegOp) : "neg"
    (x:TGArityNeOp) : "arity-ne"
    (x:TGHasStackSpace) : "has-stack-space"
